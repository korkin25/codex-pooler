defmodule CodexPooler.Gateway.OpenAICompatibility.Responses do
  @moduledoc false

  alias CodexPooler.Gateway.OpenAICompatibility.{Error, Matrix, Validation}
  alias CodexPooler.Gateway.OpenAICompatibility.Responses.{Input, SSE}

  alias CodexPooler.Gateway.Payloads.{
    InputShape,
    RequestOptions,
    StrictSchema,
    ToolResultShape,
    ToolSchemaLowering
  }

  alias CodexPooler.ServiceTier

  @reasoning_contexts ~w(auto current_turn all_turns)
  @reasoning_summaries ~w(auto concise detailed)
  @service_tiers ~w(auto default flex priority scale ultrafast)
  @truncation_modes ~w(auto disabled)
  @allowed_tools_builtin_types ~w(programmatic_tool_calling web_search_preview web_search image_generation)
  @locally_unsupported_fields ~w(background context_management conversation max_tool_calls prompt top_logprobs user)

  @endpoint "/backend-api/codex/responses"

  @spec validate(term()) :: {:ok, map()} | {:error, Error.reason()}
  def validate(payload), do: validate(payload, [])

  @spec validate(term(), map() | keyword()) :: {:ok, map()} | {:error, Error.reason()}
  def validate(payload, opts) do
    surface = surface(opts)

    with {:ok, payload} <- Validation.normalize_payload(payload),
         :ok <- Validation.reject_high_impact_fields(payload),
         :ok <- Validation.reject_unsupported_fields(payload, :responses),
         :ok <- Validation.require_model(payload),
         :ok <- reject_locally_unsupported_fields(payload),
         :ok <- validate_prompt_cache_options(payload),
         {:ok, payload} <- Input.normalize_recoverable_opencode_replay_call_ids(payload),
         {:ok, payload} <- Input.normalize_list_input(payload),
         payload = normalize_optional_function_tool_booleans(payload),
         payload = ToolSchemaLowering.lower_non_strict_function_tools(payload),
         has_tool_result? = ToolResultShape.any?(Map.get(payload, "input")),
         :ok <- Input.validate_input(payload, has_tool_result?),
         :ok <- Input.validate_previous_response_continuation(payload, has_tool_result?),
         :ok <- validate_tools(payload),
         :ok <- StrictSchema.validate_public_type_vocabulary(payload),
         :ok <- StrictSchema.validate_public_root_contract(payload),
         {:ok, payload} <- maybe_repair_direct_responses_function_tools(payload, surface),
         :ok <- validate_tool_choice(payload, surface),
         :ok <- validate_max_output_tokens(payload),
         :ok <- validate_reasoning(payload),
         :ok <- validate_moderation(payload),
         :ok <- validate_service_tier(payload),
         :ok <- validate_truncation(payload),
         :ok <- validate_stream_options(payload),
         :ok <- StrictSchema.validate_public(payload),
         :ok <- InputShape.validate(payload) do
      Input.normalize_audio_input(payload)
    end
  end

  @spec coerce(term(), map() | keyword()) ::
          {:ok, %{endpoint: String.t(), payload: map(), request_options: RequestOptions.t()}}
          | {:error, Error.reason()}
  def coerce(payload, opts \\ %{}) do
    with {:ok, payload} <- validate(payload, opts),
         {:ok, payload} <-
           payload
           |> Map.take(Matrix.forwarded_fields(:responses))
           |> normalize_forwarded_enums()
           |> Input.finalize_normalized_input() do
      payload =
        maybe_force_backend_streaming(payload, opts)

      request_options =
        opts
        |> drop_surface()
        |> RequestOptions.build(@endpoint, payload)
        |> RequestOptions.put_openai_compatibility(
          custom_tool_namespaces: custom_tool_namespaces(payload)
        )

      {:ok, %{endpoint: @endpoint, payload: payload, request_options: request_options}}
    end
  end

  defp surface(opts) when is_list(opts), do: Keyword.get(opts, :surface, :responses)
  defp surface(%RequestOptions{}), do: :responses
  defp surface(opts) when is_map(opts), do: Map.get(opts, :surface, :responses)

  defp maybe_repair_direct_responses_function_tools(payload, :responses),
    do: StrictSchema.repair_direct_responses_function_tools(payload)

  defp maybe_repair_direct_responses_function_tools(payload, :chat), do: {:ok, payload}

  defp drop_surface(opts) when is_list(opts), do: Keyword.delete(opts, :surface)
  defp drop_surface(%RequestOptions{} = opts), do: opts
  defp drop_surface(opts) when is_map(opts), do: Map.delete(opts, :surface)

  @spec response_from_sse(binary(), RequestOptions.t() | map()) ::
          {:ok, map()} | {:error, Error.reason()}
  def response_from_sse(body, request_options \\ %{}) when is_binary(body) do
    SSE.response_from_sse(body, custom_tool_namespaces(request_options))
  end

  @spec restore_custom_tool_call_namespaces(map(), map()) :: map()
  def restore_custom_tool_call_namespaces(%{} = value, custom_tool_namespaces)
      when is_map(custom_tool_namespaces) do
    value
    |> restore_nested_response(custom_tool_namespaces)
    |> restore_nested_item(custom_tool_namespaces)
    |> restore_output_items(custom_tool_namespaces)
    |> restore_custom_tool_call_namespace(custom_tool_namespaces)
  end

  defp maybe_force_backend_streaming(payload, opts) do
    if backend_streaming_required?(opts) do
      payload
      |> Map.put("stream", true)
      |> Map.put("store", false)
    else
      payload
    end
  end

  defp backend_streaming_required?(%RequestOptions{openai_compatibility: compatibility}) do
    compatibility.collect_openai_response_stream or compatibility.public_openai_responses_stream or
      compatibility.public_openai_chat_stream
  end

  defp backend_streaming_required?(opts) when is_map(opts) do
    Enum.any?(
      [
        :collect_openai_response_stream,
        :public_openai_responses_stream,
        :public_openai_chat_stream
      ],
      &Map.get(opts, &1)
    )
  end

  defp backend_streaming_required?(opts) when is_list(opts),
    do: backend_streaming_required?(Map.new(opts))

  defp custom_tool_namespaces(%RequestOptions{
         openai_compatibility: %{custom_tool_namespaces: custom_tool_namespaces}
       })
       when is_map(custom_tool_namespaces),
       do: custom_tool_namespaces

  defp custom_tool_namespaces(%{"tools" => tools}) when is_list(tools),
    do: custom_tool_namespaces_from_tools(tools)

  defp custom_tool_namespaces(%{tools: tools}) when is_list(tools),
    do: custom_tool_namespaces_from_tools(tools)

  defp custom_tool_namespaces(_value), do: %{}

  defp custom_tool_namespaces_from_tools(tools) do
    Enum.reduce(tools, %{}, fn
      %{"type" => "namespace", "name" => namespace, "tools" => children}, acc
      when is_binary(namespace) and is_list(children) ->
        Enum.reduce(children, acc, fn
          %{"type" => "custom", "name" => name}, child_acc when is_binary(name) ->
            Map.put(child_acc, name, namespace)

          _child, child_acc ->
            child_acc
        end)

      _tool, acc ->
        acc
    end)
  end

  defp restore_nested_response(%{"response" => %{} = response} = value, namespaces) do
    Map.put(value, "response", restore_custom_tool_call_namespaces(response, namespaces))
  end

  defp restore_nested_response(value, _namespaces), do: value

  defp restore_nested_item(%{"item" => %{} = item} = value, namespaces) do
    Map.put(value, "item", restore_custom_tool_call_namespaces(item, namespaces))
  end

  defp restore_nested_item(value, _namespaces), do: value

  defp restore_output_items(%{"output" => output} = value, namespaces) when is_list(output) do
    output =
      Enum.map(output, fn
        %{} = item -> restore_custom_tool_call_namespaces(item, namespaces)
        item -> item
      end)

    Map.put(value, "output", output)
  end

  defp restore_output_items(value, _namespaces), do: value

  defp restore_custom_tool_call_namespace(
         %{"type" => "custom_tool_call", "name" => name} = item,
         namespaces
       )
       when is_binary(name) do
    case {Map.get(item, "namespace"), Map.fetch(namespaces, name)} do
      {nil, {:ok, namespace}} -> Map.put(item, "namespace", namespace)
      {_provider_namespace, _mapping} -> item
    end
  end

  defp restore_custom_tool_call_namespace(value, _namespaces), do: value

  defp normalize_optional_function_tool_booleans(%{"tools" => tools} = payload)
       when is_list(tools) do
    Map.put(payload, "tools", Enum.map(tools, &normalize_optional_function_tool_booleans/1))
  end

  defp normalize_optional_function_tool_booleans(%{"type" => "function", "strict" => nil} = tool),
    do: Map.delete(tool, "strict")

  defp normalize_optional_function_tool_booleans(
         %{"type" => "namespace", "tools" => tools} = tool
       )
       when is_list(tools) do
    Map.put(tool, "tools", Enum.map(tools, &normalize_optional_function_tool_booleans/1))
  end

  defp normalize_optional_function_tool_booleans(value), do: value

  defp validate_prompt_cache_options(%{"prompt_cache_options" => options})
       when is_map(options) do
    options = Map.new(options, fn {key, value} -> {to_string(key), value} end)

    with :ok <- validate_prompt_cache_option_keys(options),
         :ok <- validate_prompt_cache_mode(Map.fetch(options, "mode")) do
      validate_prompt_cache_ttl(Map.fetch(options, "ttl"))
    end
  end

  defp validate_prompt_cache_options(%{"prompt_cache_options" => _options}),
    do:
      {:error,
       Error.invalid_request("prompt_cache_options must be an object", "prompt_cache_options")}

  defp validate_prompt_cache_options(_payload), do: :ok

  defp validate_prompt_cache_option_keys(options) do
    case options |> Map.keys() |> Enum.reject(&(&1 in ["mode", "ttl"])) |> Enum.sort() do
      [] ->
        :ok

      [key | _rest] ->
        {:error,
         Error.invalid_request(
           "prompt_cache_options field is not supported",
           "prompt_cache_options." <> key
         )}
    end
  end

  defp validate_prompt_cache_mode(:error), do: :ok
  defp validate_prompt_cache_mode({:ok, mode}) when mode in ["implicit", "explicit"], do: :ok

  defp validate_prompt_cache_mode(_mode),
    do:
      {:error,
       Error.invalid_request(
         "prompt_cache_options mode is not supported",
         "prompt_cache_options.mode"
       )}

  defp validate_prompt_cache_ttl(:error), do: :ok
  defp validate_prompt_cache_ttl({:ok, "30m"}), do: :ok

  defp validate_prompt_cache_ttl(_ttl),
    do:
      {:error,
       Error.invalid_request(
         "prompt_cache_options ttl is not supported",
         "prompt_cache_options.ttl"
       )}

  defp reject_locally_unsupported_fields(payload) do
    payload
    |> Map.keys()
    |> Enum.find(&(&1 in @locally_unsupported_fields))
    |> case do
      nil -> :ok
      field -> {:error, Error.unsupported_parameter(field)}
    end
  end

  defp validate_reasoning(%{"reasoning" => reasoning}) when is_map(reasoning) do
    reasoning
    |> Map.new(fn {key, value} -> {to_string(key), value} end)
    |> validate_reasoning_map()
  end

  defp validate_reasoning(%{"reasoning" => _reasoning}),
    do: {:error, Error.invalid_request("reasoning must be an object", "reasoning")}

  defp validate_reasoning(_payload), do: :ok

  defp validate_max_output_tokens(%{"max_output_tokens" => value})
       when is_integer(value) and value > 0,
       do: :ok

  defp validate_max_output_tokens(%{"max_output_tokens" => _value}),
    do:
      {:error,
       Error.invalid_request("max_output_tokens must be a positive integer", "max_output_tokens")}

  defp validate_max_output_tokens(_payload), do: :ok

  defp validate_reasoning_map(reasoning) do
    with :ok <- validate_reasoning_keys(reasoning),
         :ok <- validate_reasoning_effort(Map.get(reasoning, "effort")),
         :ok <- validate_reasoning_summary(Map.get(reasoning, "summary")) do
      validate_reasoning_context(Map.get(reasoning, "context"))
    end
  end

  defp validate_reasoning_keys(reasoning) do
    case reasoning |> Map.keys() |> Enum.reject(&(&1 in ["effort", "summary", "context"])) do
      [] ->
        :ok

      [key | _rest] ->
        {:error, Error.invalid_request("reasoning field is not supported", "reasoning." <> key)}
    end
  end

  defp validate_reasoning_effort(nil), do: :ok

  defp validate_reasoning_effort(effort),
    do: Validation.validate_reasoning_effort_token(effort, "reasoning.effort")

  defp validate_reasoning_summary(nil), do: :ok

  defp validate_reasoning_summary(summary) when is_binary(summary) do
    normalized = summary |> String.trim() |> String.downcase()

    if normalized in @reasoning_summaries do
      :ok
    else
      {:error, Error.invalid_request("reasoning summary is not supported", "reasoning.summary")}
    end
  end

  defp validate_reasoning_summary(_summary),
    do: {:error, Error.invalid_request("reasoning summary is not supported", "reasoning.summary")}

  defp validate_reasoning_context(nil), do: :ok

  defp validate_reasoning_context(context) when is_binary(context) do
    if normalize_enum(context) in @reasoning_contexts do
      :ok
    else
      {:error, Error.invalid_request("reasoning context is not supported", "reasoning.context")}
    end
  end

  defp validate_reasoning_context(_context),
    do: {:error, Error.invalid_request("reasoning context is not supported", "reasoning.context")}

  defp validate_moderation(%{"moderation" => moderation}) when is_map(moderation) do
    with :ok <- validate_moderation_keys(moderation),
         model when is_binary(model) <- Map.get(moderation, "model"),
         true <- String.trim(model) != "" do
      :ok
    else
      {:error, reason} ->
        {:error, reason}

      _value ->
        {:error, Error.invalid_request("moderation model is required", "moderation.model")}
    end
  end

  defp validate_moderation(%{"moderation" => _moderation}),
    do: {:error, Error.invalid_request("moderation must be an object", "moderation")}

  defp validate_moderation(_payload), do: :ok

  defp validate_moderation_keys(moderation) do
    case moderation |> Map.keys() |> Enum.reject(&(&1 == "model")) do
      [] ->
        :ok

      [key | _rest] ->
        {:error, Error.invalid_request("moderation field is not supported", "moderation." <> key)}
    end
  end

  defp validate_service_tier(%{"service_tier" => tier}) when is_binary(tier) do
    normalized = ServiceTier.canonicalize(tier)

    if normalized in @service_tiers do
      :ok
    else
      {:error, Error.invalid_request("service_tier is not supported", "service_tier")}
    end
  end

  defp validate_service_tier(%{"service_tier" => _tier}),
    do: {:error, Error.invalid_request("service_tier is not supported", "service_tier")}

  defp validate_service_tier(_payload), do: :ok

  defp validate_truncation(%{"truncation" => truncation}) when is_binary(truncation) do
    normalized = truncation |> String.trim() |> String.downcase()

    if normalized in @truncation_modes do
      :ok
    else
      {:error, Error.invalid_request("truncation is not supported", "truncation")}
    end
  end

  defp validate_truncation(%{"truncation" => _truncation}),
    do: {:error, Error.invalid_request("truncation is not supported", "truncation")}

  defp validate_truncation(_payload), do: :ok

  defp validate_stream_options(%{"stream_options" => options}) when is_map(options) do
    with :ok <- validate_stream_option_keys(options, ["include_obfuscation"]) do
      case Map.get(options, "include_obfuscation") do
        nil ->
          :ok

        value when is_boolean(value) ->
          :ok

        _value ->
          {:error,
           Error.invalid_request(
             "stream_options.include_obfuscation must be a boolean",
             "stream_options.include_obfuscation"
           )}
      end
    end
  end

  defp validate_stream_options(%{"stream_options" => _options}),
    do: {:error, Error.invalid_request("stream_options must be an object", "stream_options")}

  defp validate_stream_options(_payload), do: :ok

  defp validate_stream_option_keys(options, allowed_keys) do
    case options |> Map.keys() |> Enum.reject(&(&1 in allowed_keys)) do
      [] ->
        :ok

      [key | _rest] ->
        {:error,
         Error.invalid_request("stream_options field is not supported", "stream_options." <> key)}
    end
  end

  defp normalize_forwarded_enums(payload) do
    payload
    |> normalize_service_tier()
    |> normalize_reasoning_fields()
  end

  defp normalize_service_tier(%{"service_tier" => tier} = payload) when is_binary(tier),
    do: Map.put(payload, "service_tier", ServiceTier.canonicalize(tier))

  defp normalize_service_tier(payload), do: payload

  defp normalize_string_field(payload, field) do
    case Map.fetch(payload, field) do
      {:ok, value} when is_binary(value) -> Map.put(payload, field, normalize_enum(value))
      _other -> payload
    end
  end

  defp normalize_reasoning_fields(%{"reasoning" => reasoning} = payload) when is_map(reasoning) do
    reasoning =
      reasoning
      |> normalize_string_field("effort")
      |> normalize_string_field("summary")
      |> normalize_string_field("context")

    Map.put(payload, "reasoning", reasoning)
  end

  defp normalize_reasoning_fields(payload), do: payload

  defp normalize_enum(value) when is_binary(value),
    do: value |> String.trim() |> String.downcase()

  defp validate_tools(%{"tools" => tools}) when is_list(tools) do
    with :ok <- validate_each_tool(tools) do
      validate_unique_tool_names(tools)
    end
  end

  defp validate_tools(%{"tools" => _tools}),
    do: {:error, Error.invalid_request("tools must be an array", "tools")}

  defp validate_tools(_payload), do: :ok

  defp validate_each_tool(tools) do
    Enum.reduce_while(tools, :ok, fn tool, _acc ->
      case validate_tool(tool) do
        :ok -> {:cont, :ok}
        {:error, reason} -> {:halt, {:error, reason}}
      end
    end)
  end

  defp validate_unique_tool_names(tools) do
    executable_names = executable_tool_names(tools)
    namespace_names = namespace_tool_names(tools)

    if duplicate_name?(executable_names) or duplicate_name?(namespace_names),
      do: {:error, Error.invalid_request("tool names must be unique", "tools")},
      else: :ok
  end

  defp executable_tool_names(tools) do
    Enum.flat_map(tools, fn
      %{"type" => type, "name" => name} when type in ["function", "custom"] ->
        [name]

      %{"type" => "namespace", "tools" => namespace_tools} ->
        Enum.map(namespace_tools, &Map.fetch!(&1, "name"))

      _tool ->
        []
    end)
  end

  defp namespace_tool_names(tools) do
    Enum.flat_map(tools, fn
      %{"type" => "namespace", "name" => name} -> [name]
      _tool -> []
    end)
  end

  defp duplicate_name?(names), do: length(names) != MapSet.size(MapSet.new(names))

  defp validate_tool(%{"type" => "namespace"} = tool) do
    with :ok <- validate_exact_tool_keys(tool, ["type", "name", "description", "tools"]),
         :ok <-
           validate_nonblank_tool_field(tool, "name", "namespace tool requires a non-empty name"),
         :ok <-
           validate_nonblank_tool_field(
             tool,
             "description",
             "namespace tool requires a non-empty description"
           ) do
      validate_namespace_tools(Map.get(tool, "tools"))
    end
  end

  defp validate_tool(%{"type" => "mcp"}),
    do: {:error, Error.invalid_request("remote MCP tools are not supported", "tools")}

  defp validate_tool(%{"namespace" => _namespace}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_tool(%{"deferred" => _deferred}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_tool(%{"type" => "function"} = tool), do: validate_function_tool(tool)

  defp validate_tool(%{"type" => "custom"} = tool), do: validate_custom_tool(tool)

  defp validate_tool(%{"type" => "programmatic_tool_calling"} = tool),
    do: validate_exact_builtin_tool(tool, ["type"])

  defp validate_tool(%{"type" => "web_search_preview"} = tool),
    do: validate_exact_builtin_tool(tool, ["type"])

  defp validate_tool(%{"type" => "web_search"} = tool) do
    with :ok <-
           validate_exact_builtin_tool(tool, [
             "type",
             "external_web_access",
             "index_gated_web_access",
             "filters"
           ]),
         :ok <- validate_optional_boolean_tool_field(tool, "external_web_access"),
         :ok <- validate_optional_boolean_tool_field(tool, "index_gated_web_access"),
         :ok <- validate_optional_web_search_filters(tool) do
      validate_index_gated_web_access(tool)
    end
  end

  defp validate_tool(
         %{"type" => "image_generation", "model" => model, "size" => size, "quality" => quality} =
           tool
       )
       when is_binary(model) and is_binary(size) and is_binary(quality) do
    with :ok <- validate_image_mask(tool) do
      validate_exact_builtin_tool(tool, [
        "type",
        "model",
        "size",
        "quality",
        "background",
        "input_fidelity",
        "input_image_mask",
        "output_format"
      ])
    end
  end

  defp validate_tool(%{"type" => "image_generation"} = tool),
    do: validate_exact_builtin_tool(tool, ["type"])

  defp validate_tool(_tool),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_image_mask(%{"input_image_mask" => %{"image_url" => url} = mask})
       when is_binary(url) and byte_size(url) > 0 and map_size(mask) == 1,
       do: :ok

  defp validate_image_mask(%{"input_image_mask" => _}),
    do: {:error, Error.invalid_request("image mask requires an image_url", "tools")}

  defp validate_image_mask(_), do: :ok

  defp validate_namespace_tools(tools) when is_list(tools) and tools != [] do
    Enum.reduce_while(tools, :ok, fn tool, _acc ->
      case validate_namespace_tool(tool) do
        :ok -> {:cont, :ok}
        {:error, reason} -> {:halt, {:error, reason}}
      end
    end)
  end

  defp validate_namespace_tools(_tools),
    do:
      {:error, Error.invalid_request("namespace tool requires function or custom tools", "tools")}

  defp validate_namespace_tool(
         %{"type" => "function", "name" => name, "parameters" => parameters} = tool
       )
       when is_binary(name) and is_map(parameters) do
    validate_function_tool(tool)
  end

  defp validate_namespace_tool(%{"type" => "custom"} = tool), do: validate_custom_tool(tool)

  defp validate_namespace_tool(_tool),
    do:
      {:error, Error.invalid_request("namespace tool requires function or custom tools", "tools")}

  defp validate_function_tool(
         %{"type" => "function", "name" => name, "parameters" => parameters} = tool
       )
       when is_binary(name) and is_map(parameters) do
    with :ok <-
           validate_exact_tool_keys(tool, [
             "type",
             "name",
             "description",
             "parameters",
             "strict",
             "defer_loading",
             "allowed_callers",
             "output_schema"
           ]),
         :ok <-
           validate_nonblank_tool_field(tool, "name", "function tool requires a non-empty name"),
         :ok <- validate_optional_boolean_tool_field(tool, "strict"),
         :ok <- validate_optional_boolean_tool_field(tool, "defer_loading"),
         :ok <- validate_optional_allowed_callers(tool) do
      validate_optional_output_schema(tool)
    end
  end

  defp validate_function_tool(_tool),
    do:
      {:error, Error.invalid_request("function tool requires flat name and parameters", "tools")}

  defp validate_custom_tool(tool) do
    with :ok <-
           validate_exact_tool_keys(tool, [
             "type",
             "name",
             "description",
             "defer_loading",
             "allowed_callers",
             "format"
           ]),
         :ok <-
           validate_nonblank_tool_field(tool, "name", "custom tool requires a non-empty name"),
         :ok <- validate_optional_string_tool_field(tool, "description"),
         :ok <- validate_optional_boolean_tool_field(tool, "defer_loading"),
         :ok <- validate_optional_custom_allowed_callers(tool) do
      validate_optional_custom_format(tool)
    end
  end

  defp validate_nonblank_tool_field(tool, field, message) do
    case Map.get(tool, field) do
      value when is_binary(value) ->
        if String.trim(value) == "",
          do: {:error, Error.invalid_request(message, "tools")},
          else: :ok

      _value ->
        {:error, Error.invalid_request(message, "tools")}
    end
  end

  defp validate_optional_boolean_tool_field(tool, field) do
    case Map.fetch(tool, field) do
      {:ok, value} when is_boolean(value) -> :ok
      {:ok, _value} -> {:error, Error.invalid_request("tool shape is not translatable", "tools")}
      :error -> :ok
    end
  end

  defp validate_optional_string_tool_field(tool, field) do
    case Map.fetch(tool, field) do
      {:ok, value} when is_binary(value) -> :ok
      {:ok, _value} -> {:error, Error.invalid_request("tool shape is not translatable", "tools")}
      :error -> :ok
    end
  end

  defp validate_optional_custom_allowed_callers(%{"allowed_callers" => nil}), do: :ok

  defp validate_optional_custom_allowed_callers(%{"allowed_callers" => allowed_callers})
       when is_list(allowed_callers) do
    if Enum.all?(allowed_callers, &(&1 in ["direct", "programmatic"])),
      do: :ok,
      else: {:error, Error.invalid_request("tool shape is not translatable", "tools")}
  end

  defp validate_optional_custom_allowed_callers(%{"allowed_callers" => _allowed_callers}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_optional_custom_allowed_callers(_tool), do: :ok

  defp validate_optional_custom_format(%{"format" => %{"type" => "text"} = format}),
    do: validate_exact_tool_keys(format, ["type"])

  defp validate_optional_custom_format(%{
         "format" =>
           %{
             "type" => "grammar",
             "definition" => definition,
             "syntax" => syntax
           } = format
       })
       when is_binary(definition) and syntax in ["lark", "regex"] do
    with :ok <- validate_exact_tool_keys(format, ["type", "definition", "syntax"]) do
      if String.trim(definition) == "",
        do: {:error, Error.invalid_request("tool shape is not translatable", "tools")},
        else: :ok
    end
  end

  defp validate_optional_custom_format(%{"format" => _format}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_optional_custom_format(_tool), do: :ok

  defp validate_optional_web_search_filters(%{"filters" => filters})
       when is_map(filters) and map_size(filters) > 0 do
    with :ok <- validate_exact_tool_keys(filters, ["allowed_domains", "blocked_domains"]),
         :ok <- validate_optional_web_search_domain_list(filters, "allowed_domains") do
      validate_optional_web_search_domain_list(filters, "blocked_domains")
    end
  end

  defp validate_optional_web_search_filters(%{"filters" => _filters}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_optional_web_search_filters(_tool), do: :ok

  defp validate_optional_web_search_domain_list(filters, field) do
    case Map.fetch(filters, field) do
      {:ok, domains} when is_list(domains) and domains != [] and length(domains) <= 100 ->
        if Enum.all?(domains, &valid_web_search_domain?/1),
          do: :ok,
          else: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

      {:ok, _domains} ->
        {:error, Error.invalid_request("tool shape is not translatable", "tools")}

      :error ->
        :ok
    end
  end

  defp valid_web_search_domain?(domain) when is_binary(domain) do
    trimmed_domain = String.trim(domain)
    downcased_domain = String.downcase(trimmed_domain)

    trimmed_domain != "" and not String.starts_with?(downcased_domain, "http://") and
      not String.starts_with?(downcased_domain, "https://")
  end

  defp valid_web_search_domain?(_domain), do: false

  defp validate_optional_allowed_callers(%{"allowed_callers" => allowed_callers})
       when is_list(allowed_callers) do
    if Enum.all?(allowed_callers, &(&1 in ["direct", "programmatic"])),
      do: :ok,
      else: {:error, Error.invalid_request("tool shape is not translatable", "tools")}
  end

  defp validate_optional_allowed_callers(%{"allowed_callers" => _allowed_callers}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_optional_allowed_callers(_tool), do: :ok

  defp validate_optional_output_schema(%{"output_schema" => output_schema})
       when is_map(output_schema),
       do: :ok

  defp validate_optional_output_schema(%{"output_schema" => _output_schema}),
    do: {:error, Error.invalid_request("tool shape is not translatable", "tools")}

  defp validate_optional_output_schema(_tool), do: :ok

  defp validate_index_gated_web_access(%{"index_gated_web_access" => false}) do
    {:error, Error.invalid_request("tool shape is not translatable", "tools")}
  end

  defp validate_index_gated_web_access(%{
         "external_web_access" => false,
         "index_gated_web_access" => true
       }) do
    {:error, Error.invalid_request("tool shape is not translatable", "tools")}
  end

  defp validate_index_gated_web_access(%{"index_gated_web_access" => true} = tool) do
    if Map.has_key?(tool, "external_web_access"),
      do: :ok,
      else: {:error, Error.invalid_request("tool shape is not translatable", "tools")}
  end

  defp validate_index_gated_web_access(_tool), do: :ok

  defp validate_exact_builtin_tool(tool, allowed_keys) do
    validate_exact_tool_keys(tool, allowed_keys)
  end

  defp validate_exact_tool_keys(tool, allowed_keys) do
    case tool |> Map.keys() |> Enum.reject(&(&1 in allowed_keys)) do
      [] -> :ok
      [_key | _rest] -> {:error, Error.invalid_request("tool shape is not translatable", "tools")}
    end
  end

  defp validate_tool_choice(%{"tool_choice" => choice}, _surface)
       when choice in ["auto", "none", "required"],
       do: :ok

  defp validate_tool_choice(%{"tool_choice" => %{"type" => "allowed_tools"}}, :chat),
    do: invalid_tool_choice_shape()

  defp validate_tool_choice(
         %{"tool_choice" => %{"type" => "allowed_tools"} = choice} = payload,
         :responses
       ) do
    if valid_allowed_tools_choice?(choice, payload),
      do: :ok,
      else: invalid_tool_choice_shape()
  end

  defp validate_tool_choice(
         %{"tool_choice" => %{"type" => "function", "name" => name}} = payload,
         _surface
       )
       when is_binary(name) do
    with :ok <- validate_exact_tool_choice_keys(Map.get(payload, "tool_choice"), ["type", "name"]) do
      validate_named_tool_choice(payload, "function", name)
    end
  end

  defp validate_tool_choice(
         %{"tool_choice" => %{"type" => "custom", "name" => name}} = payload,
         _surface
       )
       when is_binary(name) do
    with :ok <- validate_exact_tool_choice_keys(Map.get(payload, "tool_choice"), ["type", "name"]) do
      validate_named_tool_choice(payload, "custom", name)
    end
  end

  defp validate_tool_choice(
         %{"tool_choice" => %{"type" => "image_generation"} = choice},
         _surface
       ),
       do: validate_exact_tool_choice_keys(choice, ["type"])

  defp validate_tool_choice(
         %{
           "tool_choice" => %{"type" => "programmatic_tool_calling"} = choice
         },
         _surface
       ),
       do: validate_exact_tool_choice_keys(choice, ["type"])

  defp validate_tool_choice(%{"tool_choice" => %{"type" => "function"}}, _surface),
    do:
      {:error,
       Error.invalid_request("tool_choice function requires a non-empty name", "tool_choice")}

  defp validate_tool_choice(%{"tool_choice" => %{"type" => "custom"}}, _surface),
    do:
      {:error,
       Error.invalid_request("tool_choice custom requires a non-empty name", "tool_choice")}

  defp validate_tool_choice(%{"tool_choice" => _choice}, _surface),
    do: invalid_tool_choice_shape()

  defp validate_tool_choice(_payload, _surface), do: :ok

  defp validate_exact_tool_choice_keys(choice, allowed_keys) do
    case choice |> Map.keys() |> Enum.reject(&(&1 in allowed_keys)) do
      [] ->
        :ok

      [_key | _rest] ->
        {:error, Error.invalid_request("tool_choice shape is not translatable", "tool_choice")}
    end
  end

  defp valid_allowed_tools_choice?(choice, payload) do
    case choice do
      %{"mode" => mode, "tools" => allowed_tools}
      when mode in ["auto", "required"] and is_list(allowed_tools) and allowed_tools != [] ->
        Map.keys(choice) |> MapSet.new() == MapSet.new(["type", "mode", "tools"]) and
          Enum.all?(allowed_tools, &allowed_tool_declared?(&1, payload))

      _choice ->
        false
    end
  end

  defp allowed_tool_declared?(%{"type" => type, "name" => name} = allowed_tool, payload)
       when type in ["function", "custom"] and is_binary(name) do
    map_size(allowed_tool) == 2 and String.trim(name) != "" and
      Enum.any?(Map.get(payload, "tools", []), fn
        %{"type" => ^type, "name" => ^name} = declared_tool ->
          Map.get(declared_tool, "defer_loading", false) == false

        _declared_tool ->
          false
      end)
  end

  defp allowed_tool_declared?(%{"type" => type} = allowed_tool, payload)
       when type in @allowed_tools_builtin_types do
    map_size(allowed_tool) == 1 and
      Enum.any?(Map.get(payload, "tools", []), &match?(%{"type" => ^type}, &1))
  end

  defp allowed_tool_declared?(_allowed_tool, _payload), do: false

  defp invalid_tool_choice_shape do
    {:error, Error.invalid_request("tool_choice shape is not translatable", "tool_choice")}
  end

  defp validate_named_tool_choice(payload, type, name) do
    cond do
      String.trim(name) == "" ->
        {:error,
         Error.invalid_request("tool_choice #{type} requires a non-empty name", "tool_choice")}

      name in named_tool_names(payload, type) ->
        :ok

      true ->
        {:error,
         Error.invalid_request("tool_choice references unknown #{type} tool", "tool_choice")}
    end
  end

  defp named_tool_names(payload, "function"), do: function_tool_names(payload)
  defp named_tool_names(payload, "custom"), do: custom_tool_names(payload)

  defp function_tool_names(%{"tools" => tools}) when is_list(tools) do
    Enum.flat_map(tools, fn
      %{"type" => "function", "name" => name} when is_binary(name) ->
        [name]

      %{"type" => "namespace", "tools" => namespace_tools} when is_list(namespace_tools) ->
        Enum.flat_map(namespace_tools, fn
          %{"type" => "function", "name" => name} when is_binary(name) -> [name]
          _tool -> []
        end)

      _tool ->
        []
    end)
  end

  defp function_tool_names(_payload), do: []

  defp custom_tool_names(%{"tools" => tools}) when is_list(tools) do
    Enum.flat_map(tools, fn
      %{"type" => "custom", "name" => name} when is_binary(name) ->
        [name]

      %{"type" => "namespace", "tools" => namespace_tools} when is_list(namespace_tools) ->
        custom_tool_names(%{"tools" => namespace_tools})

      _tool ->
        []
    end)
  end

  defp custom_tool_names(_payload), do: []
end
