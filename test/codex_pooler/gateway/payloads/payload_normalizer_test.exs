defmodule CodexPooler.Gateway.Payloads.PayloadNormalizerTest do
  use ExUnit.Case, async: false

  alias CodexPooler.AgentV2ContractFixture
  alias CodexPooler.Catalog.Model
  alias CodexPooler.Gateway.OperationalSettings
  alias CodexPooler.Gateway.Payloads.CompactionTrigger
  alias CodexPooler.Gateway.Payloads.ContinuityPayload
  alias CodexPooler.Gateway.Payloads.PayloadNormalizer
  alias CodexPooler.Gateway.Payloads.RequestOptions
  alias CodexPooler.Gateway.Payloads.RequestOptions.CompactionProjectionContext
  alias CodexPooler.Gateway.Payloads.ToolSchemaLowering
  alias CodexPooler.Gateway.Transports.UpstreamDispatch
  alias CodexPooler.Upstreams.Schemas.UpstreamIdentity

  describe "upstream_payload/4" do
    test "finalizes three-stage compaction provenance and clears transient digests" do
      downstream = %{
        "model" => "client-model",
        "previous_response_id" => "resp_projection_anchor_a",
        "input" => [
          %{"type" => "custom_tool_call_output", "output" => "raw-output-sentinel"},
          %{"type" => "compaction_trigger"}
        ]
      }

      {:ok, compact} =
        CompactionTrigger.prepare_bridge(
          "/backend-api/codex/responses",
          Map.put(downstream, "stream", true)
        )

      request_options =
        %{
          compaction_trigger_bridge?: true,
          compaction_projection_context: CompactionProjectionContext.new(downstream, compact)
        }
        |> RequestOptions.build("/backend-api/codex/responses/compact", compact)
        |> RequestOptions.put_transport(upstream_endpoint: "/backend-api/codex/responses")

      assert {:ok, encoded, normalized_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 compact,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses/compact",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["previous_response_id"] ==
               "resp_projection_anchor_a"

      assert normalized_options.payload_context.compaction_projection_context == nil

      assert normalized_options.payload_context.compaction_projection["action"] == "preserved"

      refute inspect(normalized_options.payload_context.compaction_projection) =~
               "resp_projection"

      refute inspect(normalized_options.payload_context.compaction_projection) =~ "raw-output"
    end

    test "keeps transient provenance when final JSON encoding fails" do
      downstream = %{
        "model" => "client-model",
        "previous_response_id" => "resp_projection_encode_failure",
        "input" => [%{"type" => "compaction_trigger"}]
      }

      compact =
        downstream
        |> Map.put("tools", [self()])
        |> CompactionTrigger.project_responses_payload()

      request_options =
        RequestOptions.build(
          %{
            compaction_trigger_bridge?: true,
            compaction_projection_context: CompactionProjectionContext.new(downstream, compact)
          },
          "/backend-api/codex/responses/compact",
          compact
        )

      assert {:error, %Protocol.UndefinedError{protocol: JSON.Encoder}} =
               PayloadNormalizer.prepare_upstream_payload(
                 compact,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses/compact",
                 request_options
               )

      assert %CompactionProjectionContext{} =
               request_options.payload_context.compaction_projection_context

      assert request_options.payload_context.compaction_projection == nil
    end

    @tag :prompt_cache_characterization
    test "non-Responses serialization preserves prompt cache controls and unrelated JSON structure" do
      payload = %{
        "model" => "client-model",
        "prompt_cache_key" => "cache-key-fixture",
        "prompt_cache_options" => %{"mode" => "explicit", "ttl" => "30m"},
        "input" => [
          %{
            "type" => "input_text",
            "text" => "fixture-text",
            "prompt_cache_breakpoint" => %{"mode" => "explicit"}
          },
          %{
            type: "input_image",
            image_url: "fixture-image",
            prompt_cache_breakpoint: %{mode: "explicit"}
          },
          %{
            "type" => ["input_file"],
            "file_id" => "fixture-file",
            "prompt_cache_breakpoint" => %{"mode" => "explicit"}
          }
        ],
        "nested" => %{
          "prompt_cache_options" => %{"mode" => "nested"},
          "ordered" => [3, 1, 2]
        },
        "unrelated" => %{"enabled" => true, "nullable" => nil},
        atom_only: %{prompt_cache_breakpoint: %{mode: "atom-key"}}
      }

      request_options = RequestOptions.build(%{}, "/v1/future-endpoint", payload)
      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded, _request_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 payload,
                 model,
                 "/v1/future-endpoint",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded) == %{
               "model" => "provider-model",
               "prompt_cache_key" => "cache-key-fixture",
               "prompt_cache_options" => %{"mode" => "explicit", "ttl" => "30m"},
               "input" => [
                 %{
                   "type" => "input_text",
                   "text" => "fixture-text",
                   "prompt_cache_breakpoint" => %{"mode" => "explicit"}
                 },
                 %{
                   "type" => "input_image",
                   "image_url" => "fixture-image",
                   "prompt_cache_breakpoint" => %{"mode" => "explicit"}
                 },
                 %{
                   "type" => ["input_file"],
                   "file_id" => "fixture-file",
                   "prompt_cache_breakpoint" => %{"mode" => "explicit"}
                 }
               ],
               "nested" => %{
                 "prompt_cache_options" => %{"mode" => "nested"},
                 "ordered" => [3, 1, 2]
               },
               "atom_only" => %{
                 "prompt_cache_breakpoint" => %{"mode" => "atom-key"}
               },
               "unrelated" => %{"enabled" => true, "nullable" => nil}
             }
    end

    @tag :prompt_cache_adaptation
    test "Responses serialization removes only supported prompt cache controls" do
      payload = %{
        "model" => "client-model",
        "prompt_cache_key" => "cache-key-fixture",
        "prompt_cache_options" => %{"mode" => "explicit", "ttl" => "30m"},
        "input" => [
          %{
            "type" => "message",
            "role" => "user",
            "content" => [
              %{
                "type" => "input_text",
                "text" => "fixture-text",
                "prompt_cache_breakpoint" => %{"mode" => "explicit"},
                "nested" => %{
                  "type" => "input_image",
                  "image_url" => "fixture-image",
                  "prompt_cache_breakpoint" => %{"mode" => "explicit"}
                }
              },
              %{
                "type" => "input_file",
                "file_id" => "fixture-file",
                "prompt_cache_breakpoint" => %{"mode" => "explicit"}
              }
            ]
          }
        ],
        "nested" => %{"prompt_cache_options" => %{"mode" => "preserved"}},
        "ordered" => [3, 1, 2],
        "unrelated" => %{"enabled" => true, "nullable" => nil}
      }

      model = %Model{upstream_model_id: "provider-model"}

      cases = [
        {"regular", "/backend-api/codex/responses",
         RequestOptions.build(%{}, "/backend-api/codex/responses", payload)},
        {"compact", "/backend-api/codex/responses/compact",
         RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)},
        {"websocket", "/backend-api/codex/responses",
         %{}
         |> RequestOptions.build("/backend-api/codex/responses", payload)
         |> RequestOptions.for_websocket(payload)}
      ]

      for {label, endpoint, request_options} <- cases do
        assert {:ok, encoded, updated_options} =
                 PayloadNormalizer.prepare_upstream_payload(
                   payload,
                   model,
                   endpoint,
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)

        refute Map.has_key?(upstream, "prompt_cache_options"), message: label
        refute prompt_cache_breakpoint_present?(upstream), message: label
        assert upstream["prompt_cache_key"] == "cache-key-fixture", message: label

        if label == "compact" do
          assert MapSet.new(Map.keys(upstream)) == MapSet.new(~w(model input prompt_cache_key)),
            message: label

          refute Map.has_key?(upstream, "nested"), message: label
          refute Map.has_key?(upstream, "ordered"), message: label
          refute Map.has_key?(upstream, "unrelated"), message: label
        else
          assert upstream["nested"] == %{"prompt_cache_options" => %{"mode" => "preserved"}},
            message: label

          assert upstream["ordered"] == [3, 1, 2], message: label
          assert upstream["unrelated"] == %{"enabled" => true, "nullable" => nil}, message: label
        end

        assert updated_options.runtime.prompt_cache_controls_downgraded, message: label
      end
    end

    @tag :prompt_cache_adaptation
    test "Responses serialization preserves malformed and unsupported breakpoint shapes" do
      payload = %{
        "model" => "client-model",
        "input" => [
          %{"prompt_cache_breakpoint" => %{"case" => "missing-type"}},
          %{
            "type" => "future_input",
            "prompt_cache_breakpoint" => %{"case" => "unsupported-type"}
          },
          %{
            type: "input_text",
            prompt_cache_breakpoint: %{case: "atom-keys"}
          },
          %{
            "type" => %{"value" => "input_text"},
            "prompt_cache_breakpoint" => %{"case" => "map-type"}
          },
          %{
            "type" => ["input_text"],
            "prompt_cache_breakpoint" => %{"case" => "list-type"}
          },
          %{
            "type" => 42,
            "prompt_cache_breakpoint" => %{"case" => "non-string-type"}
          }
        ]
      }

      request_options =
        RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)

      assert {:ok, encoded, updated_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses/compact",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)

      assert upstream["input"] ==
               CodexPooler.JSON.decode!(CodexPooler.JSON.encode!(payload))["input"]

      refute updated_options.runtime.prompt_cache_controls_downgraded
    end

    @tag :prompt_cache_adaptation
    test "each serialization overwrites stale prompt cache adaptation state" do
      model = %Model{upstream_model_id: "provider-model"}
      targeted_payload = %{"model" => "client-model", "prompt_cache_options" => %{}}
      clean_payload = %{"model" => "client-model", "input" => []}

      false_options =
        RequestOptions.build(%{}, "/backend-api/codex/responses", targeted_payload)

      assert {:ok, _encoded, true_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 targeted_payload,
                 model,
                 "/backend-api/codex/responses",
                 false_options
               )

      assert true_options.runtime.prompt_cache_controls_downgraded

      websocket_options = RequestOptions.for_websocket(true_options, clean_payload)

      assert {:ok, _encoded, cleared_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 clean_payload,
                 model,
                 "/backend-api/codex/responses",
                 websocket_options
               )

      refute cleared_options.runtime.prompt_cache_controls_downgraded

      targeted_options = RequestOptions.for_websocket(cleared_options, targeted_payload)

      assert {:ok, _encoded, reset_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 targeted_payload,
                 model,
                 "/backend-api/codex/responses",
                 targeted_options
               )

      assert reset_options.runtime.prompt_cache_controls_downgraded
    end

    test "materializes present malformed reasoning aliases without lower-priority fallthrough" do
      cases = [
        %{"reasoning" => %{"effort" => 42}, "reasoning_effort" => "high"},
        %{"reasoning" => %{"effort" => "  "}, "reasoning_effort" => "high"},
        %{"reasoning_effort" => %{"invalid" => true}, "reasoningEffort" => "high"},
        %{"reasoning_effort" => "  ", "reasoningEffort" => "high"},
        %{"reasoningEffort" => 42, "thinking" => "high"},
        %{"reasoningEffort" => " ", "thinking" => "high"},
        %{"thinking" => 42, "enable_thinking" => true},
        %{"thinking" => " ", "enable_thinking" => true}
      ]

      model = %Model{upstream_model_id: "provider-model"}

      for aliases <- cases do
        payload =
          Map.merge(%{"model" => "gpt-4.1", "input" => native_text_input("hello")}, aliases)

        request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        refute get_in(upstream, ["reasoning", "effort"])
        refute Map.has_key?(upstream, "reasoning_effort")
        refute Map.has_key?(upstream, "reasoningEffort")
        refute Map.has_key?(upstream, "thinking")
        refute Map.has_key?(upstream, "enable_thinking")
      end
    end

    test "removes backend Codex encrypted tool schema markers from HTTP upstream JSON" do
      payload = encrypted_tool_schema_payload()
      request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)

      assert get_in(upstream, ["tools", Access.at(0), "parameters", "properties", "message"]) ==
               %{
                 "description" => "Initial plain-text task for the new agent.",
                 "type" => "string"
               }

      assert get_in(upstream, [
               "tools",
               Access.at(1),
               "function",
               "parameters",
               "properties",
               "message"
             ]) ==
               %{
                 "description" => "Message text to queue on the target agent.",
                 "type" => "string"
               }
    end

    test "removes backend Codex encrypted tool schema markers from websocket upstream JSON" do
      payload = encrypted_tool_schema_payload()

      request_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)
      assert upstream["type"] == "response.create"

      refute Map.has_key?(
               get_in(upstream, ["tools", Access.at(0), "parameters", "properties", "message"]),
               "encrypted"
             )

      refute Map.has_key?(
               get_in(upstream, [
                 "tools",
                 Access.at(1),
                 "function",
                 "parameters",
                 "properties",
                 "message"
               ]),
               "encrypted"
             )
    end

    @tag :schema_position_aware
    test "cleans flat non-strict schema marker keywords without deleting encrypted property or definition names" do
      payload = schema_position_aware_tool_payload()
      model = %Model{upstream_model_id: "provider-model"}

      options_by_transport = [
        http: RequestOptions.build(%{}, "/backend-api/codex/responses", payload),
        compact: RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload),
        websocket:
          %{}
          |> RequestOptions.build("/backend-api/codex/responses", payload)
          |> RequestOptions.for_websocket(payload)
      ]

      for {transport, request_options} <- options_by_transport do
        endpoint =
          if transport == :compact,
            do: "/backend-api/codex/responses/compact",
            else: "/backend-api/codex/responses"

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, request_options)

        parameters =
          CodexPooler.JSON.decode!(encoded) |> get_in(["tools", Access.at(0), "parameters"])

        refute Map.has_key?(parameters, "encrypted"), "unexpected root marker for #{transport}"

        assert parameters["required"] == ["encrypted", "nested"],
               "required drift for #{transport}"

        assert get_in(parameters, ["properties", "encrypted", "properties", "encrypted"]) ==
                 %{"type" => "string"},
               "property name drift for #{transport}"

        assert get_in(parameters, ["$defs", "encrypted"]) == %{"type" => "string"},
               "$defs name drift for #{transport}"

        assert get_in(parameters, ["definitions", "encrypted"]) == %{"type" => "number"},
               "definitions name drift for #{transport}"

        assert get_in(parameters, ["items", "properties", "encrypted"]) == %{"type" => "boolean"},
               "items property drift for #{transport}"

        assert get_in(parameters, ["anyOf", Access.at(0), "properties", "encrypted"]) ==
                 %{"type" => "string"},
               "composition property drift for #{transport}"

        assert get_in(parameters, ["oneOf", Access.at(0), "properties", "encrypted"]) ==
                 %{"type" => "integer"},
               "oneOf property drift for #{transport}"

        assert get_in(parameters, ["allOf", Access.at(0), "properties", "encrypted"]) ==
                 %{"type" => "null"},
               "allOf property drift for #{transport}"
      end
    end

    @tag :schema_position_aware
    test "keeps malformed schema-position branches bounded without deleting encrypted names" do
      payload = %{
        "model" => "gpt-5.5",
        "input" => [%{"role" => "user", "content" => "hello"}],
        "tools" => [
          %{
            "type" => "function",
            "name" => "malformed_schema_position_fixture",
            "strict" => false,
            "parameters" => %{
              "type" => "object",
              "encrypted" => %{"unexpected" => true},
              "properties" => %{"encrypted" => ["not-a-schema"]},
              "$defs" => %{"encrypted" => 42},
              "definitions" => %{"encrypted" => nil},
              "items" => ["not-a-schema"],
              "anyOf" => %{"not" => "a-list"},
              "unknown" => %{"encrypted" => true}
            }
          }
        ]
      }

      model = %Model{upstream_model_id: "provider-model"}

      for {endpoint, request_options} <- [
            {"/backend-api/codex/responses",
             RequestOptions.build(%{}, "/backend-api/codex/responses", payload)},
            {"/backend-api/codex/responses/compact",
             RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)},
            {"/backend-api/codex/responses",
             %{}
             |> RequestOptions.build("/backend-api/codex/responses", payload)
             |> RequestOptions.for_websocket(payload)}
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, request_options)

        parameters =
          CodexPooler.JSON.decode!(encoded) |> get_in(["tools", Access.at(0), "parameters"])

        assert Map.has_key?(parameters["properties"], "encrypted")
        assert Map.has_key?(parameters["$defs"], "encrypted")
        assert Map.has_key?(parameters["definitions"], "encrypted")
      end
    end

    test "lowers backend Codex non-strict function tool schemas for HTTP and websocket upstream JSON" do
      payload = non_strict_tool_schema_payload()
      model = %Model{upstream_model_id: "provider-model"}

      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      websocket_options = RequestOptions.for_websocket(http_options, payload)

      for request_options <- [http_options, websocket_options] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)

        assert get_in(upstream, ["tools", Access.at(0), "parameters"]) ==
                 lowered_tool_schema()

        assert get_in(upstream, ["tools", Access.at(1), "function", "parameters"]) ==
                 lowered_tool_schema()

        assert get_in(upstream, ["tools", Access.at(2), "parameters"]) ==
                 non_strict_tool_schema()
      end
    end

    test "preserves backend Codex strict function schema terms for HTTP and websocket upstream JSON" do
      strict_tool = %{
        "type" => "function",
        "name" => "strict_lookup",
        "strict" => true,
        "parameters" => %{
          "type" => "object",
          "encrypted" => true,
          "additionalProperties" => false,
          "properties" => %{
            "config" => %{
              "type" => "future-native-token",
              "unknown_schema_key" => %{"preserve" => [1, nil, false]}
            },
            "encrypted" => %{"type" => "string", "encrypted" => true}
          },
          "required" => ["config", "encrypted"],
          "unknown_root_key" => true
        },
        "unknown_tool_key" => %{"preserve" => true}
      }

      payload = %{
        "model" => "gpt-5.5",
        "input" => [%{"role" => "user", "content" => "hello"}],
        "tools" => [strict_tool]
      }

      model = %Model{upstream_model_id: "provider-model"}
      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      websocket_options = RequestOptions.for_websocket(http_options, payload)
      compact_options = RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)

      for {endpoint, request_options} <- [
            {"/backend-api/codex/responses", http_options},
            {"/backend-api/codex/responses/compact", compact_options},
            {"/backend-api/codex/responses", websocket_options}
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   endpoint,
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["tools"] == [strict_tool]
      end
    end

    test "preserves backend Codex namespace subtrees while lowering ordinary function tools" do
      namespace_tool = backend_namespace_tool()
      payload = backend_mixed_tool_payload(namespace_tool)
      model = %Model{upstream_model_id: "provider-model"}

      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      websocket_options = RequestOptions.for_websocket(http_options, payload)

      for request_options <- [http_options, websocket_options] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        assert Enum.at(upstream["tools"], 0) == namespace_tool

        assert get_in(upstream, ["tools", Access.at(1), "parameters"]) ==
                 lowered_backend_function_schema()

        assert Enum.at(upstream["tools"], 1)["encrypted"]
      end
    end

    test "removes backend Codex encrypted-only agent messages from websocket upstream JSON" do
      payload = %{
        "model" => "gpt-5.5",
        "input" => [
          %{"type" => "message", "role" => "user", "content" => "hello"},
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [
              %{"type" => "encrypted_content", "encrypted_content" => "opaque-agent-message"}
            ]
          },
          %{
            "type" => "message",
            "role" => "assistant",
            "content" => nil,
            "encrypted_content" => "preserved-assistant-replay"
          },
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [%{"type" => "output_text", "text" => "clear agent message"}]
          }
        ]
      }

      request_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)

      assert upstream["input"] == [
               %{"type" => "message", "role" => "user", "content" => "hello"},
               %{
                 "type" => "message",
                 "role" => "assistant",
                 "content" => nil,
                 "encrypted_content" => "preserved-assistant-replay"
               },
               %{
                 "type" => "agent_message",
                 "author" => "root",
                 "recipient" => "worker",
                 "content" => [%{"type" => "output_text", "text" => "clear agent message"}]
               }
             ]
    end

    test "preserves backend Codex plaintext input_text agent messages while stripping encrypted-only siblings" do
      payload = %{
        "model" => "gpt-5.5",
        "input" => [
          %{"type" => "message", "role" => "user", "content" => "hello"},
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [
              %{"type" => "encrypted_content", "encrypted_content" => "opaque-agent-message"}
            ]
          },
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [%{"type" => "input_text", "text" => "synthetic agent note"}]
          }
        ]
      }

      request_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)

      assert upstream["input"] == [
               %{"type" => "message", "role" => "user", "content" => "hello"},
               %{
                 "type" => "agent_message",
                 "author" => "root",
                 "recipient" => "worker",
                 "content" => [%{"type" => "input_text", "text" => "synthetic agent note"}]
               }
             ]
    end

    test "removes backend Codex mixed encrypted agent messages from websocket upstream JSON" do
      payload = %{
        "model" => "gpt-5.5",
        "input" => [
          %{"type" => "message", "role" => "user", "content" => "hello"},
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [
              %{"type" => "input_text", "text" => "Message Type: MESSAGE\nPayload:\n"},
              %{"type" => "encrypted_content", "encrypted_content" => "opaque-agent-message"}
            ]
          },
          %{
            "type" => "message",
            "role" => "assistant",
            "content" => nil,
            "encrypted_content" => "preserved-assistant-replay"
          },
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [%{"type" => "input_text", "text" => "clear agent message"}]
          }
        ]
      }

      request_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)

      assert Enum.map(upstream["input"], &Map.fetch!(&1, "type")) == [
               "message",
               "message",
               "agent_message"
             ]

      assert get_in(upstream, ["input", Access.at(1), "encrypted_content"]) ==
               "preserved-assistant-replay"

      assert get_in(upstream, ["input", Access.at(2), "content", Access.at(0), "type"]) ==
               "input_text"
    end

    @tag :encrypted_reasoning_continuity
    test "retains current encrypted reasoning across HTTP and websocket normalization" do
      reasoning = %{
        "type" => "reasoning",
        "content" => nil,
        "encrypted_content" => "synthetic-current-reasoning"
      }

      payload = %{"model" => "gpt-5.5", "input" => [reasoning]}
      model = %Model{upstream_model_id: "provider-model"}
      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      websocket_options = RequestOptions.for_websocket(http_options, payload)

      assert {:ok, http_encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 http_options
               )

      assert {:ok, websocket_encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 websocket_options
               )

      refute Map.has_key?(Map.from_struct(http_options.continuity), :protected_replay?)
      refute Map.has_key?(Map.from_struct(websocket_options.continuity), :protected_replay?)
      assert CodexPooler.JSON.decode!(http_encoded)["input"] == [reasoning]
      assert CodexPooler.JSON.decode!(websocket_encoded)["input"] == [reasoning]
    end

    @tag :encrypted_reasoning_continuity
    test "characterizes ordinary prompt cache key routing as HTTP soft locality only" do
      raw_key = "synthetic-ordinary-prompt-cache-key"
      payload = %{"model" => "gpt-5.5", "input" => "hello", "prompt_cache_key" => raw_key}
      expected_hash = :crypto.hash(:sha256, raw_key) |> Base.encode16(case: :lower)

      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      websocket_options = RequestOptions.for_websocket(http_options, payload)

      assert http_options.routing.prompt_cache_key == expected_hash
      assert websocket_options.routing.prompt_cache_key == nil
      refute Map.has_key?(Map.from_struct(http_options.continuity), :protected_prompt_cache_key)

      refute Map.has_key?(
               Map.from_struct(websocket_options.continuity),
               :protected_prompt_cache_key
             )
    end

    @tag :encrypted_reasoning_continuity
    test "strips malformed encrypted reasoning shapes from HTTP and websocket upstream JSON" do
      invalid_reasoning_items = [
        %{"type" => "reasoning", "content" => [], "encrypted_content" => "opaque"},
        %{"type" => "reasoning", "encrypted_content" => "opaque"},
        %{"type" => "reasoning", "content" => "non-null", "encrypted_content" => "opaque"},
        %{"type" => "reasoning", "content" => nil, "encrypted_content" => "   "}
      ]

      Enum.each(invalid_reasoning_items, fn item ->
        refute ContinuityPayload.current_encrypted_reasoning?(item)

        payload = %{"model" => "gpt-5.5", "input" => [item]}

        options = [
          RequestOptions.build(%{}, "/backend-api/codex/responses", payload),
          RequestOptions.for_websocket(%{}, payload)
        ]

        Enum.each(options, fn options ->
          assert {:ok, encoded} =
                   PayloadNormalizer.upstream_payload(
                     payload,
                     %Model{upstream_model_id: "provider-model"},
                     "/backend-api/codex/responses",
                     options
                   )

          assert CodexPooler.JSON.decode!(encoded)["input"] == []
        end)
      end)
    end

    @tag :encrypted_reasoning_continuity
    test "recognizes only the canonical v2 encrypted handoff shape" do
      handoff = AgentV2ContractFixture.handoff!(:send_message)

      assert ContinuityPayload.v2_encrypted_handoff?(handoff)
    end

    test "preserves malformed agent message content shapes while removing encrypted markers" do
      payload = %{
        "model" => "gpt-5.5",
        "input" => [
          %{"type" => "agent_message", "content" => nil},
          %{"type" => "agent_message", "content" => "not-a-list"},
          %{
            "type" => "agent_message",
            "content" => ["odd-part", %{"type" => "input_text", "text" => "clear note"}]
          },
          %{
            "type" => "agent_message",
            "content" => [%{"encrypted_content" => "opaque-agent-message"}]
          }
        ]
      }

      request_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)

      assert Enum.map(upstream["input"], &Map.fetch!(&1, "type")) == [
               "agent_message",
               "agent_message",
               "agent_message"
             ]

      assert Enum.map(upstream["input"], &Map.get(&1, "content")) |> Enum.map(&content_shape/1) ==
               [nil, :binary, :list]
    end

    test "preserves canonical v2 encrypted handoffs while removing other mixed encrypted agent messages" do
      new_task_handoff = AgentV2ContractFixture.handoff!(:spawn_agent)
      message_handoff = AgentV2ContractFixture.handoff!(:send_message)
      followup_handoff = AgentV2ContractFixture.handoff!(:followup_task)
      v1_handoff = AgentV2ContractFixture.v1_handoff!()
      plaintext_handoff = AgentV2ContractFixture.plaintext_handoff!()

      payload = %{
        "input" => [
          new_task_handoff,
          message_handoff,
          followup_handoff,
          v1_handoff,
          plaintext_handoff,
          %{
            "type" => "agent_message",
            "author" => "/root",
            "recipient" => "/root/worker",
            "content" => [
              %{"type" => "input_text", "text" => "Message Type: MESSAGE\nPayload:\n"},
              %{"type" => "encrypted_content", "encrypted_content" => "opaque-agent-message"}
            ]
          },
          %{
            "type" => "agent_message",
            "author" => "root",
            "recipient" => "worker",
            "content" => [
              %{
                "type" => "input_text",
                "text" => "Message Type: NEW_TASK\nTask name: worker\nSender: root\nPayload:\n"
              },
              %{"type" => "encrypted_content", "encrypted_content" => "relative-path-lookalike"}
            ]
          },
          %{
            "type" => "agent_message",
            "author" => "/root",
            "recipient" => "/root/../worker",
            "content" => [
              %{
                "type" => "input_text",
                "text" =>
                  "Message Type: NEW_TASK\nTask name: /root/../worker\nSender: /root\nPayload:\n"
              },
              %{"type" => "encrypted_content", "encrypted_content" => "invalid-agent-path"}
            ]
          },
          %{
            "type" => "agent_message",
            "author" => "/root",
            "recipient" => "/root/worker",
            "content" => [
              %{
                "type" => "input_text",
                "text" =>
                  "Message Type: NEW_TASK\nTask name: /root/other\nSender: /root\nPayload:\n"
              },
              %{"type" => "encrypted_content", "encrypted_content" => "mismatched-binding"}
            ]
          },
          %{
            "type" => "agent_message",
            "content" => [%{"type" => "input_text", "text" => "plain keeper only"}]
          }
        ]
      }

      malformed_payload = %{
        "input" => %{
          "type" => "agent_message",
          "content" => [%{"type" => "encrypted_content", "encrypted_content" => "opaque"}]
        }
      }

      assert {:ok,
              %{
                "input" => [
                  ^new_task_handoff,
                  ^message_handoff,
                  ^followup_handoff,
                  ^v1_handoff,
                  ^plaintext_handoff,
                  %{
                    "type" => "agent_message",
                    "content" => [%{"type" => "input_text", "text" => "plain keeper only"}]
                  }
                ]
              }} = PayloadNormalizer.normalize(payload)

      assert {:ok, ^malformed_payload} = PayloadNormalizer.normalize(malformed_payload)
    end

    test "pins the current source-derived agent handoff contract" do
      fixture = AgentV2ContractFixture.load!()

      assert fixture["contract_version"] == 2

      assert get_in(fixture, ["source", "commit"]) ==
               "c9c6c0daa994109cec50fddcb57d076fdf9e738c"

      assert get_in(fixture, ["v2", "handoffs", "spawn_agent", "message_type"]) == "NEW_TASK"

      assert get_in(fixture, ["v2", "handoffs", "send_message", "message_type"]) ==
               "MESSAGE"

      assert get_in(fixture, ["v2", "handoffs", "followup_task", "message_type"]) ==
               "NEW_TASK"

      assert AgentV2ContractFixture.plaintext_function_call!()["encrypted_function_args"] == []

      assert AgentV2ContractFixture.final_answer!() ==
               "Message Type: FINAL_ANSWER\nTask name: /root\nSender: /root/worker\nPayload:\nSYNTHETIC_FINAL_ANSWER_SENTINEL"
    end

    test "preserves source-derived collaboration namespace definitions byte-for-byte" do
      namespace_tools = AgentV2ContractFixture.namespace_tools!()

      payload = %{
        "model" => "gpt-5.5",
        "input" => [%{"role" => "user", "content" => "hello"}],
        "tools" => namespace_tools
      }

      request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      model = %Model{upstream_model_id: "provider-model"}

      compact_options =
        RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)

      websocket_options = RequestOptions.for_websocket(request_options, payload)

      for {endpoint, options} <- [
            {"/backend-api/codex/responses", request_options},
            {"/backend-api/codex/responses/compact", compact_options},
            {"/backend-api/codex/responses", websocket_options}
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, options)

        assert CodexPooler.JSON.decode!(encoded)["tools"] == namespace_tools
      end
    end

    @tag :schema_position_aware
    test "keeps the v1 handoff unchanged through each ordinary flat-function normalization lane" do
      v1_handoff = AgentV2ContractFixture.v1_handoff!()
      payload = schema_position_aware_tool_payload() |> Map.put("input", [v1_handoff])
      model = %Model{upstream_model_id: "provider-model"}

      for {endpoint, request_options} <- [
            {"/backend-api/codex/responses",
             RequestOptions.build(%{}, "/backend-api/codex/responses", payload)},
            {"/backend-api/codex/responses/compact",
             RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)},
            {"/backend-api/codex/responses",
             %{}
             |> RequestOptions.build("/backend-api/codex/responses", payload)
             |> RequestOptions.for_websocket(payload)}
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, request_options)

        assert CodexPooler.JSON.decode!(encoded)["input"] == [v1_handoff]
      end
    end

    test "omits neutral tiers, canonicalizes binary fast, and preserves other backend tiers" do
      model = %Model{upstream_model_id: "provider-model"}

      for payload <- [
            %{"model" => "gpt-4.1", "input" => native_text_input("hello")},
            %{
              "model" => "gpt-4.1",
              "input" => native_text_input("hello"),
              "service_tier" => "auto"
            },
            %{
              "model" => "gpt-4.1",
              "input" => native_text_input("hello"),
              "service_tier" => "default"
            }
          ] do
        request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        refute Map.has_key?(CodexPooler.JSON.decode!(encoded), "service_tier")
      end

      for tier <- ["priority", " PRIORITY ", "flex", "scale", "latency_preview", " "] do
        payload = %{
          "model" => "gpt-4.1",
          "input" => native_text_input("hello"),
          "service_tier" => tier
        }

        request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["service_tier"] == tier
      end

      for tier <- ["fast", " FAST ", "Fast"] do
        payload = %{
          "model" => "gpt-4.1",
          "input" => native_text_input("hello"),
          "service_tier" => tier
        }

        request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["service_tier"] == "priority"
      end

      for tier <- [123, nil, ["fast"], %{"id" => "fast"}] do
        payload = %{
          "model" => "gpt-4.1",
          "input" => native_text_input("hello"),
          "service_tier" => tier
        }

        request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["service_tier"] == tier
      end
    end

    test "keeps raw continuation options separate from final HTTP and websocket payloads" do
      previous_response_id = "  response-fixture  "

      payload = %{
        "model" => "gpt-5.5",
        "previous_response_id" => previous_response_id,
        "input" => [%{"type" => "message", "role" => "user", "content" => "hello"}]
      }

      model = %Model{upstream_model_id: "provider-model"}

      http_options =
        RequestOptions.build(
          %{previous_response_id: previous_response_id},
          "/backend-api/codex/responses",
          payload
        )

      assert {:ok, http_encoded, normalized_http_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 http_options
               )

      refute Map.has_key?(CodexPooler.JSON.decode!(http_encoded), "previous_response_id")
      assert normalized_http_options.continuity.previous_response_id == previous_response_id

      websocket_options = RequestOptions.for_websocket(http_options, payload)

      assert {:ok, websocket_encoded, normalized_websocket_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 websocket_options
               )

      assert CodexPooler.JSON.decode!(websocket_encoded)["previous_response_id"] ==
               previous_response_id

      assert normalized_websocket_options.continuity.previous_response_id == previous_response_id
    end

    test "derives previous response state from only the final normalized payload" do
      model = %Model{upstream_model_id: "provider-model"}

      cases = [
        {:valid_websocket, "  response-fixture  ", :websocket, ordinary_input(), true, true},
        {:blank_websocket, "  ", :websocket, ordinary_input(), true, false},
        {:non_binary_websocket, 42, :websocket, ordinary_input(), true, false},
        {:stripped_http, "response-fixture", :http, ordinary_input(), false, false},
        {:retained_semantic_http, "response-fixture", :http, tool_result_input(), true, true},
        {:retained_standalone_http, "response-fixture", :http, standalone_tool_result_input(),
         true, true}
      ]

      for {label, previous_response_id, transport, input, final_id_present?, expected_marker} <-
            cases do
        payload = %{
          "model" => "gpt-5.5",
          "previous_response_id" => previous_response_id,
          "input" => input
        }

        options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        options =
          if transport == :websocket,
            do: RequestOptions.for_websocket(options, payload),
            else: options

        assert {:ok, encoded, normalized_options} =
                 PayloadNormalizer.prepare_upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   options
                 )

        assert Map.has_key?(CodexPooler.JSON.decode!(encoded), "previous_response_id") ==
                 final_id_present?,
               message: "case: #{label}"

        assert normalized_options.continuity.upstream_previous_response_id? == expected_marker,
          message: "case: #{label}"

        assert is_boolean(normalized_options.continuity.upstream_previous_response_id?),
          message: "case: #{label}"
      end
    end

    test "derives connection-bound state on the actual upstream websocket request struct" do
      payload = %{
        "model" => "gpt-5.5",
        "previous_response_id" => "response-fixture",
        "input" => ordinary_input()
      }

      native_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      public_options =
        %{
          openai_source_endpoint: "/v1/responses",
          public_openai_responses_stream: true
        }
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      assert {true, true} = capture_continuation_state(payload, native_options)
      assert {true, false} = capture_continuation_state(payload, public_options)
    end

    test "retained semantic native HTTP state is not connection-bound at dispatch" do
      payload = %{
        "model" => "gpt-5.5",
        "previous_response_id" => "response-fixture",
        "input" => tool_result_input()
      }

      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

      assert {true, false} = capture_continuation_state(payload, http_options)
    end

    test "carries gateway debug metadata on request options instead of process state" do
      previous_env = Application.get_env(:codex_pooler, OperationalSettings)

      Application.put_env(:codex_pooler, OperationalSettings,
        settings: %OperationalSettings{gateway_debug?: true}
      )

      on_exit(fn ->
        if previous_env,
          do: Application.put_env(:codex_pooler, OperationalSettings, previous_env),
          else: Application.delete_env(:codex_pooler, OperationalSettings)
      end)

      request_options =
        RequestOptions.build(
          %{request_id: "payload-debug-explicit"},
          "/backend-api/codex/responses",
          %{"model" => "gpt-4.1", "input" => native_text_input("hello")}
        )

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded, updated_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 %{"model" => "gpt-4.1", "input" => native_text_input("hello")},
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["model"] == "provider-model"

      assert %{
               "request_id" => "payload-debug-explicit",
               "transport" => "http_json"
             } = updated_options.runtime.gateway_debug_payload

      refute Process.get({:codex_gateway_debug_payload, "payload-debug-explicit"})
    end

    test "preserves the effective image model on marked native generation and edit routes" do
      model = %Model{upstream_model_id: "provider-text-model"}

      for endpoint <- [
            "/backend-api/codex/images/generations",
            "/backend-api/codex/images/edits"
          ],
          effective_model <- ["gpt-image-2", "future-image-model-fixture"] do
        payload = %{"model" => "client-controlled-model", "input" => native_text_input("hello")}

        request_options =
          RequestOptions.build(
            %{native_image_request?: true, effective_model: effective_model},
            endpoint,
            payload
          )

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, request_options)

        assert CodexPooler.JSON.decode!(encoded)["model"] == effective_model
      end
    end

    test "keeps the host model outside marked native image routes" do
      model = %Model{upstream_model_id: "provider-text-model"}

      for {endpoint, options} <- [
            {"/backend-api/codex/responses",
             %{native_image_request?: true, effective_model: "gpt-image-2"}},
            {"/backend-api/codex/images/generations", %{effective_model: "gpt-image-2"}},
            {"/backend-api/codex/images/generations", %{native_image_request?: true}},
            {"/backend-api/codex/images/edits",
             %{native_image_request?: true, effective_model: ""}}
          ] do
        payload = %{"model" => "client-controlled-model", "input" => native_text_input("hello")}
        request_options = RequestOptions.build(options, endpoint, payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, request_options)

        assert CodexPooler.JSON.decode!(encoded)["model"] == "provider-text-model"
      end
    end

    test "returns a gateway error when a transcription upload path is unreadable" do
      request_options =
        RequestOptions.build(
          %{
            media_upload: %{
              path: Path.join(System.tmp_dir!(), "codex-pooler-missing-upload"),
              redacted_filename: "upload",
              content_type: "audio/wav",
              size: 12
            }
          },
          "/backend-api/transcribe",
          %{"model" => "gpt-4o-transcribe"}
        )

      model = %Model{upstream_model_id: "provider-transcribe"}

      assert PayloadNormalizer.upstream_payload(
               %{"model" => "gpt-4o-transcribe"},
               model,
               "/backend-api/transcribe",
               request_options
             ) ==
               {:error,
                %{
                  status: 400,
                  code: "invalid_request",
                  message: "file upload is not readable",
                  param: "file"
                }}
    end

    test "emits file-first repeated transcription array parts in exact order" do
      upload_path = transcription_upload_fixture()

      request_options =
        RequestOptions.build(
          %{
            media_upload: %{
              path: upload_path,
              redacted_filename: "audio.wav",
              content_type: "audio/wav",
              size: 15
            }
          },
          "/backend-api/transcribe",
          %{}
        )
        |> RequestOptions.mark_openai_compatibility_origin(
          "/v1/audio/transcriptions",
          "/backend-api/transcribe"
        )

      payload = %{
        "model" => "gpt-4o-transcribe",
        "prompt" => "Keep this prompt",
        "keywords" => ["first", "repeat", "repeat"],
        "languages" => ["it", "en", "it"],
        "language" => "ignored",
        "response_format" => "ignored",
        "temperature" => "ignored"
      }

      model = %Model{upstream_model_id: "provider-transcribe"}

      assert {:ok, {:multipart, [file_part | fields]}, normalized_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 payload,
                 model,
                 "/backend-api/transcribe",
                 request_options
               )

      assert {:file,
              {%File.Stream{}, [filename: "audio.wav", content_type: "audio/wav", size: 15]}} =
               file_part

      assert fields == [
               {:prompt, "Keep this prompt"},
               {"keywords[]", "first"},
               {"keywords[]", "repeat"},
               {"keywords[]", "repeat"},
               {"languages[]", "it"},
               {"languages[]", "en"},
               {"languages[]", "it"}
             ]

      refute Enum.any?([file_part | fields], fn {name, _value} ->
               to_string(name) in ["model", "language", "response_format", "temperature"]
             end)

      assert normalized_options == request_options
      refute Map.has_key?(normalized_options.request_metadata, :keywords)
      refute Map.has_key?(normalized_options.request_metadata, :languages)
      assert normalized_options.runtime.gateway_debug_payload == nil
    end

    test "emits no transcription array parts for empty lists and keeps nonblank prompt behavior" do
      upload_path = transcription_upload_fixture()

      request_options =
        RequestOptions.build(
          %{
            media_upload: %{
              path: upload_path,
              redacted_filename: "audio.wav",
              content_type: "audio/wav",
              size: 15
            }
          },
          "/backend-api/transcribe",
          %{}
        )
        |> RequestOptions.mark_openai_compatibility_origin(
          "/v1/audio/transcriptions",
          "/backend-api/transcribe"
        )

      model = %Model{upstream_model_id: "provider-transcribe"}

      for prompt <- [nil, "", "   "] do
        assert {:ok, {:multipart, [{:file, _file}]}} =
                 PayloadNormalizer.upstream_payload(
                   %{
                     "model" => "gpt-4o-transcribe",
                     "prompt" => prompt,
                     "keywords" => [],
                     "languages" => []
                   },
                   model,
                   "/backend-api/transcribe",
                   request_options
                 )
      end
    end

    test "rejects malformed prevalidated transcription arrays without a partial multipart result" do
      upload_path = transcription_upload_fixture()

      request_options =
        RequestOptions.build(
          %{
            media_upload: %{
              path: upload_path,
              redacted_filename: "audio.wav",
              content_type: "audio/wav",
              size: 15
            }
          },
          "/backend-api/transcribe",
          %{}
        )
        |> RequestOptions.mark_openai_compatibility_origin(
          "/v1/audio/transcriptions",
          "/backend-api/transcribe"
        )

      model = %Model{upstream_model_id: "provider-transcribe"}

      for {field, value} <- [{"keywords", "not-a-list"}, {"languages", ["en", 42]}] do
        expected_message = "#{field} must be a list of strings"

        assert {:error,
                %{
                  status: 400,
                  code: "invalid_request",
                  message: ^expected_message,
                  param: ^field
                }} =
                 PayloadNormalizer.upstream_payload(
                   %{"model" => "gpt-4o-transcribe", field => value},
                   model,
                   "/backend-api/transcribe",
                   request_options
                 )
      end
    end

    test "normalizes max and ultra thinking aliases to backend reasoning effort" do
      model = %Model{upstream_model_id: "provider-model"}

      for effort <- ["max", "ultra"] do
        payload = %{
          "model" => "gpt-5.6-sol",
          "input" => native_text_input("hello"),
          "thinking" => effort
        }

        request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "max"}
      end
    end

    test "maps minimal reasoning effort to low before backend dispatch" do
      payload = %{
        "model" => "gpt-4.1",
        "input" => native_text_input("hello"),
        "reasoning" => %{"effort" => "minimal"}
      }

      request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "low"}
    end

    test "passes none reasoning effort through unchanged" do
      payload = %{
        "model" => "gpt-4.1",
        "input" => native_text_input("hello"),
        "reasoning" => %{"effort" => "none"}
      }

      request_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "none"}
    end

    test "maps client-facing ultra reasoning effort to max for backend Codex HTTP, compact, and websocket JSON" do
      payload = %{
        "model" => "gpt-4.1",
        "input" => native_text_input("hello"),
        "reasoning" => %{"effort" => "ultra"}
      }

      model = %Model{upstream_model_id: "provider-model"}

      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      compact_options = RequestOptions.build(%{}, "/backend-api/codex/responses/compact", payload)
      websocket_options = RequestOptions.for_websocket(http_options, payload)

      for request_options <- [http_options, compact_options, websocket_options] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   request_options.transport.upstream_endpoint,
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "max"}
      end
    end

    test "adds required Responses Lite controls for HTTP, compact, and websocket JSON" do
      payload = %{
        "model" => "gpt-5.6-terra",
        "input" => native_text_input("hello"),
        "parallel_tool_calls" => true,
        "reasoning" => %{"effort" => "max", "summary" => "auto"}
      }

      model = %Model{upstream_model_id: "provider-model"}

      http_options =
        RequestOptions.build(
          serving_mode_opts("lite"),
          "/backend-api/codex/responses",
          payload
        )

      compact_options =
        RequestOptions.build(
          serving_mode_opts("lite"),
          "/backend-api/codex/responses/compact",
          payload
        )

      websocket_options = RequestOptions.for_websocket(http_options, payload)

      for request_options <- [http_options, compact_options, websocket_options] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   request_options.transport.upstream_endpoint,
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)

        assert upstream["reasoning"] == %{
                 "context" => "all_turns",
                 "effort" => "max",
                 "summary" => "auto"
               }

        assert upstream["parallel_tool_calls"] == false
      end
    end

    test "adds Responses Lite reasoning context when the client omits reasoning" do
      payload = %{"model" => "gpt-5.6-terra", "input" => native_text_input("hello")}

      request_options =
        RequestOptions.build(
          serving_mode_opts("lite"),
          "/backend-api/codex/responses",
          payload
        )

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses",
                 request_options
               )

      upstream = CodexPooler.JSON.decode!(encoded)
      assert upstream["reasoning"] == %{"context" => "all_turns"}
      assert upstream["parallel_tool_calls"] == false
    end

    test "full mode preserves parallel_tool_calls tri-state and removes Lite-only shaping" do
      model = %Model{upstream_model_id: "provider-model"}

      for endpoint <- [
            "/backend-api/codex/responses",
            "/backend-api/codex/responses/compact"
          ],
          {name, parallel_fields} <- [
            {"absent", %{}},
            {"true", %{"parallel_tool_calls" => true}},
            {"false", %{"parallel_tool_calls" => false}}
          ] do
        payload =
          Map.merge(
            %{
              "model" => "gpt-5.6-terra",
              "instructions" => "preserve separately",
              "tools" => [%{"type" => "custom", "name" => "lookup"}],
              "input" => [%{"type" => "message", "role" => "user", "content" => []}],
              "reasoning" => %{"effort" => "high", "context" => "current_turn"},
              "client_metadata" => %{
                "trace" => "safe-test-value",
                "ws_request_header_x_openai_internal_codex_responses_lite" => "true"
              }
            },
            parallel_fields
          )

        options = RequestOptions.build(serving_mode_opts("full"), endpoint, payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, options)

        upstream = CodexPooler.JSON.decode!(encoded)
        label = "#{endpoint}, #{name}"

        assert Map.has_key?(upstream, "parallel_tool_calls") ==
                 Map.has_key?(parallel_fields, "parallel_tool_calls"),
               label

        assert Map.get(upstream, "parallel_tool_calls") ==
                 Map.get(parallel_fields, "parallel_tool_calls"),
               label

        assert upstream["instructions"] == payload["instructions"], label
        assert upstream["tools"] == payload["tools"], label
        assert upstream["input"] == payload["input"], label
        assert get_in(upstream, ["reasoning", "context"]) == "current_turn", label

        if endpoint == "/backend-api/codex/responses/compact" do
          refute Map.has_key?(upstream, "client_metadata"), label
        else
          assert upstream["client_metadata"] == %{"trace" => "safe-test-value"}, label
        end
      end
    end

    test "native Responses rejects scalar input and non-list tools for Full HTTP and websocket" do
      for {payload, param} <- [
            {%{"model" => "gpt-5.6-terra", "input" => "synthetic scalar input"}, "input"},
            {%{
               "model" => "gpt-5.6-terra",
               "input" => [],
               "tools" => "synthetic non-list tools"
             }, "tools"}
          ] do
        options =
          RequestOptions.build(serving_mode_opts("full"), "/backend-api/codex/responses", payload)

        for request_options <- [options, RequestOptions.for_websocket(options, payload)] do
          assert {:error,
                  %{
                    status: 400,
                    code: "invalid_request",
                    param: ^param
                  }} =
                   PayloadNormalizer.upstream_payload(
                     payload,
                     %Model{upstream_model_id: "provider-model"},
                     "/backend-api/codex/responses",
                     request_options
                   )
        end
      end
    end

    test "effective snapshot survives compact and websocket retargeting without legacy flag control" do
      payload = %{
        "model" => "gpt-5.6-terra",
        "input" => native_text_input("hello"),
        "parallel_tool_calls" => true,
        "client_metadata" => %{
          "ws_request_header_x_openai_internal_codex_responses_lite" => "client-value"
        }
      }

      lite_options =
        RequestOptions.build(serving_mode_opts("lite"), "/backend-api/codex/responses", payload)

      for request_options <- [
            RequestOptions.retarget(
              lite_options,
              "/backend-api/codex/responses/compact",
              payload
            ),
            RequestOptions.for_websocket(lite_options, payload)
          ] do
        assert RequestOptions.model_serving_mode_snapshot(request_options) == %{
                 configured_mode: "lite",
                 effective_mode: "lite",
                 source: "override"
               }

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   %Model{upstream_model_id: "provider-model"},
                   request_options.transport.upstream_endpoint,
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        assert upstream["parallel_tool_calls"] == false
        assert get_in(upstream, ["reasoning", "context"]) == "all_turns"

        if request_options.transport.transport == "websocket" do
          assert get_in(upstream, [
                   "client_metadata",
                   "ws_request_header_x_openai_internal_codex_responses_lite"
                 ]) == "true"
        end
      end
    end

    test "full websocket envelopes scrub client Lite metadata without changing payload fields" do
      payload = %{
        "model" => "gpt-5.6-terra",
        "input" => [%{"type" => "message", "role" => "user", "content" => []}],
        "instructions" => "keep websocket instructions",
        "tools" => [%{"type" => "custom", "name" => "lookup"}],
        "parallel_tool_calls" => true,
        "reasoning" => %{"context" => "current_turn"},
        "client_metadata" => %{
          "trace" => "safe-test-value",
          "ws_request_header_x_openai_internal_codex_responses_lite" => "true"
        }
      }

      options =
        serving_mode_opts("full")
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.for_websocket(payload)

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 options.transport.upstream_endpoint,
                 options
               )

      upstream = CodexPooler.JSON.decode!(encoded)
      assert upstream["parallel_tool_calls"] == true
      assert upstream["instructions"] == payload["instructions"]
      assert upstream["tools"] == payload["tools"]
      assert upstream["input"] == payload["input"]
      assert upstream["reasoning"]["context"] == "current_turn"
      assert upstream["client_metadata"] == %{"trace" => "safe-test-value"}
    end

    test "normalizes the final non-compact reasoning and encrypted include envelope" do
      model = %Model{upstream_model_id: "provider-model"}

      capability_cases = [
        {"absent capability", %{}, true},
        {"true capability", %{supports_reasoning_summary_parameter?: true}, true},
        {"false capability", %{supports_reasoning_summary_parameter?: false}, false}
      ]

      include_cases = [
        {"missing include", %{}, ["reasoning.encrypted_content"]},
        {"non-list include", %{"include" => "unsupported"}, ["reasoning.encrypted_content"]},
        {"absent encrypted include", %{"include" => ["output_text.logprobs"]},
         ["output_text.logprobs", "reasoning.encrypted_content"]},
        {"duplicate encrypted include",
         %{
           "include" => [
             "output_text.logprobs",
             "reasoning.encrypted_content",
             "message.input_image.image_url",
             "reasoning.encrypted_content"
           ]
         },
         [
           "output_text.logprobs",
           "reasoning.encrypted_content",
           "message.input_image.image_url"
         ]}
      ]

      for {capability_name, option_fields, preserves_summary?} <- capability_cases,
          {include_name, include_fields, expected_include} <- include_cases do
        payload =
          Map.merge(
            %{
              "model" => "gpt-5.6-terra",
              "input" => native_text_input("hello"),
              "reasoning" => %{
                "effort" => "high",
                "summary" => "auto",
                "context" => "selected",
                "owner_policy" => "preserved"
              }
            },
            include_fields
          )

        options = RequestOptions.build(option_fields, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        label = "#{capability_name}, #{include_name}"

        assert upstream["include"] == expected_include, label
        assert upstream["reasoning"]["effort"] == "high", label
        assert upstream["reasoning"]["context"] == "selected", label
        assert upstream["reasoning"]["owner_policy"] == "preserved", label

        assert Map.has_key?(upstream["reasoning"], "summary") == preserves_summary?, label
      end

      for reasoning <- [nil, "unsupported", ["unsupported"]] do
        payload = %{
          "model" => "gpt-5.6-terra",
          "input" => native_text_input("hello"),
          "reasoning" => reasoning
        }

        options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        assert upstream["reasoning"] == %{}
        assert upstream["include"] == ["reasoning.encrypted_content"]
      end
    end

    test "normal non-compact reasoning envelopes are idempotent after JSON serialization" do
      model = %Model{upstream_model_id: "provider-model"}

      for supports_summary? <- [true, false],
          reasoning <- [
            %{"effort" => "high", "summary" => "auto", "context" => "selected"},
            nil,
            "unsupported"
          ] do
        payload = %{
          "model" => "gpt-5.6-terra",
          "input" => native_text_input("hello"),
          "include" => [
            "output_text.logprobs",
            "reasoning.encrypted_content",
            "reasoning.encrypted_content"
          ],
          "reasoning" => reasoning
        }

        options =
          RequestOptions.build(
            %{supports_reasoning_summary_parameter?: supports_summary?},
            "/backend-api/codex/responses",
            payload
          )

        assert {:ok, first_encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   options
                 )

        first = CodexPooler.JSON.decode!(first_encoded)

        second_options =
          RequestOptions.build(
            %{supports_reasoning_summary_parameter?: supports_summary?},
            "/backend-api/codex/responses",
            first
          )

        assert {:ok, second_encoded} =
                 PayloadNormalizer.upstream_payload(
                   first,
                   model,
                   "/backend-api/codex/responses",
                   second_options
                 )

        assert CodexPooler.JSON.decode!(second_encoded) == first
        assert first["include"] == ["output_text.logprobs", "reasoning.encrypted_content"]
        assert is_map(first["reasoning"])

        assert Map.has_key?(first["reasoning"], "summary") ==
                 (supports_summary? and is_map(reasoning))
      end
    end

    test "normalizes non-compact Responses Lite tools and instructions idempotently" do
      existing_prefix = %{
        "type" => "additional_tools",
        "role" => "developer",
        "tools" => [%{"type" => "custom", "name" => "existing"}]
      }

      request_item = %{
        "id" => "request_tools_1",
        "type" => "additional_tools",
        "role" => "developer",
        "tools" => [%{"type" => "custom", "name" => "request_item"}]
      }

      populated_tool = %{
        "type" => "function",
        "name" => "lookup",
        "strict" => false,
        "parameters" => %{
          "properties" => %{
            "query" => %{"type" => "string", "encrypted" => true}
          },
          "required" => ["query"]
        }
      }

      user_message = %{
        "type" => "message",
        "role" => "user",
        "content" => [
          %{
            "type" => "input_image",
            "image_url" => "data:image/png;base64,AA==",
            "detail" => "high"
          },
          %{"type" => "input_text", "text" => "hello", "detail" => "keep"}
        ]
      }

      tool_output = %{
        "type" => "function_call_output",
        "call_id" => "call_1",
        "output" => [
          %{
            "type" => "input_image",
            "image_url" => "data:image/png;base64,AA==",
            "detail" => "original"
          },
          %{"type" => "input_text", "text" => "result", "detail" => "keep"}
        ]
      }

      custom_tool_output = %{
        "type" => "custom_tool_call_output",
        "call_id" => "call_2",
        "output" => %{
          "content" => [
            %{
              "type" => "input_image",
              "image_url" => "data:image/png;base64,AA==",
              "detail" => "high"
            }
          ],
          "detail" => "keep"
        }
      }

      cases = [
        {"absent tools reuses canonical prefix", %{"input" => [existing_prefix, user_message]}, 2,
         existing_prefix["tools"]},
        {"absent tools creates empty prefix",
         %{"instructions" => "  ", "input" => [user_message]}, 2, []},
        {"empty tools creates prefix before existing prefix",
         %{"tools" => [], "input" => [existing_prefix, request_item, user_message]}, 4, []},
        {"populated tools creates lowered prefix before existing prefix",
         %{
           "tools" => [populated_tool],
           "instructions" => "  Follow the tool contract.  ",
           "input" => [
             existing_prefix,
             request_item,
             user_message,
             tool_output,
             custom_tool_output
           ]
         }, 7, [populated_tool]}
      ]

      for {name, fields, expected_prefix_count, _source_tools} <- cases do
        payload = Map.merge(%{"model" => "gpt-5.6-terra"}, fields)
        first = prepare_lite_payload(payload)
        second = prepare_lite_payload(first)

        assert second == first, name
        refute Map.has_key?(first, "tools"), name
        refute Map.has_key?(first, "instructions"), name
        assert first["parallel_tool_calls"] == false, name
        assert get_in(first, ["reasoning", "context"]) == "all_turns", name

        [prefix | input] = first["input"]
        assert prefix["type"] == "additional_tools", name
        assert prefix["role"] == "developer", name
        refute Map.has_key?(prefix, "id"), name
        assert length(first["input"]) == expected_prefix_count, name

        if Map.has_key?(fields, "tools") do
          expected_tools =
            fields
            |> Map.fetch!("tools")
            |> then(&%{"tools" => &1})
            |> ToolSchemaLowering.lower_non_strict_function_tools()
            |> Map.fetch!("tools")
            |> Enum.map(&remove_encrypted_markers/1)

          assert prefix["tools"] == expected_tools, name
        end

        if is_binary(fields["instructions"]) and String.trim(fields["instructions"]) != "" do
          [instruction | preserved] = input

          assert instruction == %{
                   "type" => "message",
                   "role" => "developer",
                   "content" => [
                     %{"type" => "input_text", "text" => fields["instructions"]}
                   ]
                 }

          assert preserved ==
                   [
                     existing_prefix,
                     request_item,
                     strip_image_detail(user_message),
                     strip_image_detail(tool_output),
                     strip_image_detail(custom_tool_output)
                   ]
        end
      end
    end

    test "ordinary Lite continuations retain the tools prefix when previous_response_id is nonblank" do
      endpoint = "/backend-api/codex/responses"

      payload = %{
        "type" => "response.create",
        "model" => "gpt-5.6-terra",
        "previous_response_id" => "resp_ordinary_lite_continuation_0001",
        "input" => [
          %{
            "type" => "function_call_output",
            "call_id" => "call_ordinary_lite_continuation",
            "output" => "synthetic output"
          }
        ],
        "tools" => [
          %{
            "type" => "function",
            "name" => "sample_lookup",
            "parameters" => %{"type" => "object", "properties" => %{}}
          }
        ],
        "stream" => true,
        "generate" => true
      }

      http_options = RequestOptions.build(serving_mode_opts("lite"), endpoint, payload)

      assert http_options.payload_context.compaction_input_mode == :incremental
      refute http_options.payload_context.compaction_trigger_bridge?

      for {transport, request_options} <- [
            http: http_options,
            websocket: RequestOptions.for_websocket(http_options, payload)
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   %Model{upstream_model_id: "provider-model"},
                   endpoint,
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)

        assert [
                 %{
                   "type" => "additional_tools",
                   "role" => "developer",
                   "tools" => [%{"name" => "sample_lookup"}]
                 },
                 %{"type" => "function_call_output"}
               ] = upstream["input"],
               "ordinary #{transport} continuation lost its tools prefix"
      end
    end

    test "preserves typed custom tool choice for full, rejects it for Lite, and keeps Lite scalar choices" do
      endpoint = "/backend-api/codex/responses"
      custom_tool = %{"type" => "custom", "name" => "custom_choice_fixture"}
      tool_choice = %{"type" => "custom", "name" => custom_tool["name"]}

      payload = %{
        "model" => "gpt-5.6-terra",
        "input" => [],
        "tools" => [custom_tool],
        "tool_choice" => tool_choice
      }

      full_http_options = RequestOptions.build(serving_mode_opts("full"), endpoint, payload)

      for request_options <- [
            full_http_options,
            RequestOptions.for_websocket(full_http_options, payload)
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   %Model{upstream_model_id: "provider-model"},
                   endpoint,
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        assert upstream["tools"] == [custom_tool]
        assert upstream["tool_choice"] == tool_choice
        refute Enum.any?(upstream["input"], &(&1["type"] == "additional_tools"))
      end

      lite_http_options = RequestOptions.build(serving_mode_opts("lite"), endpoint, payload)

      for request_options <- [
            lite_http_options,
            RequestOptions.for_websocket(lite_http_options, payload)
          ] do
        assert {:error,
                %{
                  status: 400,
                  code: "unsupported_parameter",
                  message: "Unsupported parameter: tool_choice",
                  param: "tool_choice"
                }} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   %Model{upstream_model_id: "provider-model"},
                   endpoint,
                   request_options
                 )
      end

      scalar_choice_payload = Map.put(payload, "tool_choice", "auto")

      scalar_lite_http_options =
        RequestOptions.build(serving_mode_opts("lite"), endpoint, scalar_choice_payload)

      for request_options <- [
            scalar_lite_http_options,
            RequestOptions.for_websocket(scalar_lite_http_options, scalar_choice_payload)
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   scalar_choice_payload,
                   %Model{upstream_model_id: "provider-model"},
                   endpoint,
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        assert upstream["tool_choice"] == "auto"
      end
    end

    test "rewrites compact Lite bodies without forwarding include or top-level tools" do
      payload = %{
        "model" => "gpt-5.6-terra",
        "instructions" => "compact instructions",
        "include" => ["reasoning.encrypted_content"],
        "tools" => [%{"type" => "custom", "name" => "compact_tool"}],
        "input" => [
          %{
            "type" => "message",
            "role" => "user",
            "content" => [
              %{
                "type" => "input_image",
                "image_url" => "data:image/png;base64,AA==",
                "detail" => "high"
              }
            ]
          }
        ]
      }

      first = prepare_lite_payload(payload, "/backend-api/codex/responses/compact")
      second = prepare_lite_payload(first, "/backend-api/codex/responses/compact")

      assert second == first
      refute Map.has_key?(first, "include")
      refute Map.has_key?(first, "instructions")
      refute Map.has_key?(first, "tools")

      assert first["input"] == [
               %{
                 "type" => "additional_tools",
                 "role" => "developer",
                 "tools" => payload["tools"]
               },
               %{
                 "type" => "message",
                 "role" => "developer",
                 "content" => [%{"type" => "input_text", "text" => payload["instructions"]}]
               },
               %{
                 "type" => "message",
                 "role" => "user",
                 "content" => [
                   %{
                     "type" => "input_image",
                     "image_url" => "data:image/png;base64,AA=="
                   }
                 ]
               }
             ]

      assert first["parallel_tool_calls"] == false
      assert get_in(first, ["reasoning", "context"]) == "all_turns"

      alias_options =
        serving_mode_opts("lite")
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.put_transport(upstream_endpoint: "/backend-api/codex/responses/compact")

      assert {:ok, alias_encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses",
                 alias_options
               )

      assert CodexPooler.JSON.decode!(alias_encoded) == first
    end

    test "preserves incremental compact input exactly when projecting Responses Lite" do
      function_output = %{
        "type" => "function_call_output",
        "call_id" => "call_incremental_fixture",
        "output" => [
          %{
            "type" => "input_image",
            "image_url" => "data:image/png;base64,AA==",
            "detail" => "high"
          },
          %{"type" => "input_text", "text" => "synthetic-result", "detail" => "keep"}
        ]
      }

      custom_output = %{
        "type" => "custom_tool_call_output",
        "call_id" => "call_custom_incremental_fixture",
        "output" => %{
          "content" => [
            %{
              "type" => "input_image",
              "image_url" => "data:image/png;base64,AA==",
              "detail" => "original"
            }
          ],
          "detail" => "keep"
        }
      }

      trigger = %{"type" => "compaction_trigger"}

      for {name, input, expected_input} <- [
            {"function output", [function_output, trigger],
             [strip_image_detail(function_output), trigger]},
            {"trigger only", [trigger], [trigger]},
            {"future custom output", [custom_output, trigger],
             [strip_image_detail(custom_output), trigger]}
          ] do
        source_payload = %{
          "model" => "gpt-5.6-terra",
          "previous_response_id" => "resp_incremental_projection_fixture",
          "stream" => true,
          "instructions" => "incremental instructions must not become input",
          "tools" => [%{"type" => "custom", "name" => "incremental_tool_fixture"}],
          "input" => input
        }

        {first, second, request_options} = prepare_incremental_lite_compact(source_payload)

        assert request_options.payload_context.compaction_input_mode == :incremental, name
        assert first["previous_response_id"] == source_payload["previous_response_id"], name
        assert second == first, name
        assert Enum.map(first["input"], & &1["type"]) == Enum.map(input, & &1["type"]), name
        assert length(first["input"]) == length(input), name
        assert first["input"] == expected_input, name
        assert first["store"] == false, name
        assert first["stream"] == true, name
        assert first["parallel_tool_calls"] == false, name
        assert get_in(first, ["reasoning", "context"]) == "all_turns", name
        refute Map.has_key?(first, "tools"), name
        refute Map.has_key?(first, "instructions"), name

        refute Enum.any?(first["input"], fn item ->
                 item["type"] == "additional_tools" or item["role"] == "developer"
               end),
               name
      end
    end

    test "keeps already supplied incremental Lite items in place without duplicating prefixes" do
      supplied_prefix = %{
        "type" => "additional_tools",
        "role" => "developer",
        "tools" => [%{"type" => "custom", "name" => "supplied_fixture"}]
      }

      supplied_message = %{
        "type" => "message",
        "role" => "user",
        "content" => [
          %{
            "type" => "input_image",
            "image_url" => "data:image/png;base64,AA==",
            "detail" => "high"
          }
        ]
      }

      trigger = %{"type" => "compaction_trigger"}

      source_payload = %{
        "model" => "gpt-5.6-terra",
        "previous_response_id" => "resp_existing_input_fixture",
        "stream" => true,
        "instructions" => "nonblank incremental instructions",
        "tools" => [%{"type" => "custom", "name" => "top_level_fixture"}],
        "input" => [supplied_prefix, supplied_message, trigger]
      }

      {first, second, request_options} = prepare_incremental_lite_compact(source_payload)

      assert request_options.payload_context.compaction_input_mode == :incremental
      assert second == first

      assert first["input"] == [supplied_prefix, strip_image_detail(supplied_message), trigger]
      assert Enum.count(first["input"], &(&1["type"] == "additional_tools")) == 1
      assert first["previous_response_id"] == source_payload["previous_response_id"]
      refute Map.has_key?(first, "tools")
      refute Map.has_key?(first, "instructions")
    end

    test "uses the pre-dispatch applied effort for compact payloads without re-deciding policy" do
      payload = %{"model" => "gpt-4.1", "input" => "hello"}

      decision = %CodexPooler.Access.APIKeys.ReasoningEffortPolicy.Decision{
        mode: :allow_up_to,
        configured_effort: "high",
        requested_effort: nil,
        applied_effort: "medium"
      }

      request_options =
        RequestOptions.build(
          %{
            reasoning_effort_decision: decision,
            api_key_policy: %{enforced_reasoning_effort: "ultra"}
          },
          "/backend-api/codex/responses/compact",
          payload
        )

      assert {:ok, encoded, updated_options} =
               PayloadNormalizer.prepare_upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses/compact",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "medium"}

      assert updated_options.runtime.reasoning_effort_snapshot == %{
               "applied_effort" => "medium",
               "configured_effort" => "high",
               "effective_effort" => "medium",
               "policy_mode" => "allow_up_to",
               "source" => "api_key_policy"
             }
    end

    test "preserves unrestricted explicit and omitted effort decisions" do
      model = %Model{upstream_model_id: "provider-model"}

      for {requested, payload, expected_reasoning} <- [
            {"high",
             %{
               "model" => "gpt-4.1",
               "input" => native_text_input("hello"),
               "reasoning" => %{"effort" => "high"}
             }, %{"effort" => "high"}},
            {nil, %{"model" => "gpt-4.1", "input" => native_text_input("hello")}, %{}}
          ] do
        decision = %CodexPooler.Access.APIKeys.ReasoningEffortPolicy.Decision{
          mode: :unrestricted,
          configured_effort: nil,
          requested_effort: requested,
          applied_effort: requested
        }

        options =
          RequestOptions.build(
            %{reasoning_effort_decision: decision},
            "/backend-api/codex/responses",
            payload
          )

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   options
                 )

        assert CodexPooler.JSON.decode!(encoded)["reasoning"] == expected_reasoning
        assert CodexPooler.JSON.decode!(encoded)["include"] == ["reasoning.encrypted_content"]
      end
    end

    test "ignores stale enforced policy when attributing unrestricted decisions" do
      model = %Model{upstream_model_id: "provider-model"}

      for {requested, payload, expected_source} <- [
            {"high",
             %{
               "model" => "gpt-4.1",
               "input" => native_text_input("hello"),
               "reasoning" => %{"effort" => "high"}
             }, "client"},
            {nil, %{"model" => "gpt-4.1", "input" => native_text_input("hello")}, nil}
          ] do
        decision = %CodexPooler.Access.APIKeys.ReasoningEffortPolicy.Decision{
          mode: :unrestricted,
          configured_effort: nil,
          requested_effort: requested,
          applied_effort: requested
        }

        options =
          RequestOptions.build(
            %{
              reasoning_effort_decision: decision,
              api_key_policy: %{enforced_reasoning_effort: "ultra"}
            },
            "/backend-api/codex/responses",
            payload
          )

        assert {:ok, encoded, updated_options} =
                 PayloadNormalizer.prepare_upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   options
                 )

        upstream_reasoning = CodexPooler.JSON.decode!(encoded)["reasoning"]
        snapshot = updated_options.runtime.reasoning_effort_snapshot

        assert get_in(upstream_reasoning || %{}, ["effort"]) == requested
        assert snapshot["source"] == expected_source
        assert snapshot["policy_mode"] == "unrestricted"
      end
    end

    test "always-use decision overwrites an explicit client effort" do
      payload = %{
        "model" => "gpt-4.1",
        "input" => native_text_input("hello"),
        "reasoning" => %{"effort" => "low"}
      }

      decision = %CodexPooler.Access.APIKeys.ReasoningEffortPolicy.Decision{
        mode: :always_use,
        configured_effort: "ultra",
        requested_effort: "low",
        applied_effort: "ultra"
      }

      options =
        RequestOptions.build(
          %{reasoning_effort_decision: decision},
          "/backend-api/codex/responses",
          payload
        )

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses",
                 options
               )

      assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "max"}
    end

    test "maps legacy directly-normalized enforced ultra effort to backend max" do
      payload = %{
        "model" => "gpt-4.1",
        "input" => native_text_input("hello"),
        "reasoning" => %{"effort" => "low"}
      }

      request_options =
        RequestOptions.build(
          %{api_key_policy: %{enforced_reasoning_effort: "ultra"}},
          "/backend-api/codex/responses",
          payload
        )

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["reasoning"] == %{"effort" => "max"}
    end

    test "captures reasoning effort snapshot variants on request options" do
      model = %Model{upstream_model_id: "provider-model"}

      cases = [
        {%{
           "model" => "gpt-4.1",
           "input" => native_text_input("hello"),
           "reasoning" => %{"effort" => "minimal"}
         }, %{},
         %{
           "requested_effort" => "minimal",
           "applied_effort" => "minimal",
           "effective_effort" => "low",
           "source" => "client",
           "rewrite" => "minimal_to_low"
         }},
        {%{
           "model" => "gpt-4.1",
           "input" => native_text_input("hello"),
           "reasoning" => %{"effort" => "ultra"}
         }, %{},
         %{
           "requested_effort" => "ultra",
           "applied_effort" => "ultra",
           "effective_effort" => "max",
           "source" => "client",
           "rewrite" => "ultra_to_max"
         }},
        {%{
           "model" => "gpt-4.1",
           "input" => native_text_input("hello"),
           "reasoning" => %{"effort" => "low"}
         }, %{api_key_policy: %{enforced_reasoning_effort: "ultra"}},
         %{
           "requested_effort" => "low",
           "applied_effort" => "ultra",
           "effective_effort" => "max",
           "source" => "api_key_policy",
           "rewrite" => "ultra_to_max"
         }},
        {%{
           "model" => "gpt-4.1",
           "input" => native_text_input("hello"),
           "reasoning" => %{"effort" => "none"}
         }, %{},
         %{
           "requested_effort" => "none",
           "applied_effort" => "none",
           "effective_effort" => "none",
           "source" => "client"
         }}
      ]

      for {payload, opts, expected_snapshot} <- cases do
        request_options = RequestOptions.build(opts, "/backend-api/codex/responses", payload)

        assert {:ok, _encoded, updated_options} =
                 PayloadNormalizer.prepare_upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert updated_options.runtime.reasoning_effort_snapshot == expected_snapshot
      end
    end

    test "omits enforced auto and default service tiers from upstream JSON" do
      model = %Model{upstream_model_id: "provider-model"}

      for tier <- ["auto", "default"] do
        request_options =
          RequestOptions.build(
            %{api_key_policy: %{enforced_service_tier: tier}},
            "/backend-api/codex/responses",
            %{
              "model" => "gpt-4.1",
              "input" => native_text_input("hello"),
              "service_tier" => "priority"
            }
          )

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   %{
                     "model" => "gpt-4.1",
                     "input" => native_text_input("hello"),
                     "service_tier" => "priority"
                   },
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        refute Map.has_key?(CodexPooler.JSON.decode!(encoded), "service_tier")
      end
    end

    test "omits requested auto and default service tiers from upstream JSON" do
      model = %Model{upstream_model_id: "provider-model"}

      for tier <- ["auto", "default"] do
        request_options =
          RequestOptions.build(
            %{},
            "/backend-api/codex/responses",
            %{"model" => "gpt-4.1", "input" => native_text_input("hello"), "service_tier" => tier}
          )

        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   %{
                     "model" => "gpt-4.1",
                     "input" => native_text_input("hello"),
                     "service_tier" => tier
                   },
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        refute Map.has_key?(CodexPooler.JSON.decode!(encoded), "service_tier")
      end
    end

    test "preserves explicit enforced service tiers upstream" do
      request_options =
        RequestOptions.build(
          %{api_key_policy: %{enforced_service_tier: "priority"}},
          "/backend-api/codex/responses",
          %{
            "model" => "gpt-4.1",
            "input" => native_text_input("hello"),
            "service_tier" => "default"
          }
        )

      model = %Model{upstream_model_id: "provider-model"}

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 %{
                   "model" => "gpt-4.1",
                   "input" => native_text_input("hello"),
                   "service_tier" => "default"
                 },
                 model,
                 "/backend-api/codex/responses",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["service_tier"] == "priority"
    end

    test "canonicalizes an enforced binary fast tier after it overrides the client tier" do
      payload = %{
        "model" => "gpt-4.1",
        "input" => native_text_input("hello"),
        "service_tier" => "latency_preview"
      }

      request_options =
        RequestOptions.build(
          %{api_key_policy: %{enforced_service_tier: "fast"}},
          "/backend-api/codex/responses",
          payload
        )

      assert {:ok, encoded} =
               PayloadNormalizer.upstream_payload(
                 payload,
                 %Model{upstream_model_id: "provider-model"},
                 "/backend-api/codex/responses",
                 request_options
               )

      assert CodexPooler.JSON.decode!(encoded)["service_tier"] == "priority"
    end

    test "sanitizes backend Codex optional response item IDs for HTTP and websocket" do
      input = [
        %{"type" => "message", "id" => "msg-1", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "msg_1", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "msg_a_b", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "_1", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "msg_", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => 123, "role" => "assistant", "content" => []},
        %{"type" => "message", "role" => "assistant", "content" => []},
        "preserved-list-element",
        %{"type" => "item_reference", "id" => "legacy-reference"},
        %{
          "type" => "message",
          "id" => "msg-2",
          "role" => "assistant",
          "content" => [%{"type" => "output_text", "id" => "nested-legacy", "text" => "ok"}]
        },
        %{
          "type" => "function_call_output",
          "id" => "fco-1",
          "call_id" => "call_1",
          "output" => "done"
        }
      ]

      expected_input = [
        %{"type" => "message", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "msg_1", "role" => "assistant", "content" => []},
        %{"type" => "message", "id" => "msg_a_b", "role" => "assistant", "content" => []},
        %{"type" => "message", "role" => "assistant", "content" => []},
        %{"type" => "message", "role" => "assistant", "content" => []},
        %{"type" => "message", "role" => "assistant", "content" => []},
        %{"type" => "message", "role" => "assistant", "content" => []},
        %{"type" => "message", "role" => "assistant", "content" => []},
        "preserved-list-element",
        %{"type" => "item_reference", "id" => "legacy-reference"},
        %{
          "type" => "message",
          "role" => "assistant",
          "content" => [%{"type" => "output_text", "id" => "nested-legacy", "text" => "ok"}]
        },
        %{"type" => "function_call_output", "call_id" => "call_1", "output" => "done"}
      ]

      payload = %{"model" => "gpt-5.5", "input" => input}
      model = %Model{upstream_model_id: "provider-model"}
      http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

      options_by_transport = [
        http: http_options,
        websocket: RequestOptions.for_websocket(http_options, payload)
      ]

      for {transport, request_options} <- options_by_transport do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        upstream = CodexPooler.JSON.decode!(encoded)
        assert upstream["input"] == expected_input, "unexpected #{transport} input"
      end
    end

    test "preserves null IDs only on trusted public compaction replay items" do
      input = [
        %{
          "type" => "compaction",
          "encrypted_content" => "synthetic-compaction-without-id"
        },
        %{
          "type" => "compaction",
          "encrypted_content" => "synthetic-compaction-with-id",
          "id" => "cmp_fixture"
        },
        %{
          "type" => "compaction",
          "encrypted_content" => "synthetic-compaction-with-null-id",
          "id" => nil
        },
        %{"type" => "message", "role" => "assistant", "content" => [], "id" => nil}
      ]

      payload = %{"model" => "gpt-5.5", "input" => input}
      model = %Model{upstream_model_id: "provider-model"}

      public_options =
        %{}
        |> RequestOptions.build("/backend-api/codex/responses", payload)
        |> RequestOptions.mark_openai_compatibility_origin(
          "/v1/responses",
          "/backend-api/codex/responses"
        )

      expected_public_input = [
        Enum.at(input, 0),
        Enum.at(input, 1),
        Enum.at(input, 2),
        %{"type" => "message", "role" => "assistant", "content" => []}
      ]

      for request_options <- [
            public_options,
            RequestOptions.for_websocket(public_options, payload)
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["input"] == expected_public_input
      end

      native_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)
      expected_native_input = List.update_at(expected_public_input, 2, &Map.delete(&1, "id"))

      for request_options <- [
            native_options,
            RequestOptions.for_websocket(native_options, payload)
          ] do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(
                   payload,
                   model,
                   "/backend-api/codex/responses",
                   request_options
                 )

        assert CodexPooler.JSON.decode!(encoded)["input"] == expected_native_input
      end
    end

    test "rejects non-list backend Codex input and leaves missing input unchanged" do
      model = %Model{upstream_model_id: "provider-model"}

      invalid_payload = %{"model" => "gpt-5.5", "input" => %{"id" => "msg-1"}}
      missing_payload = %{"model" => "gpt-5.5", "metadata" => %{"id" => "msg-1"}}

      for payload <- [invalid_payload, missing_payload] do
        http_options = RequestOptions.build(%{}, "/backend-api/codex/responses", payload)

        for request_options <- [http_options, RequestOptions.for_websocket(http_options, payload)] do
          result =
            PayloadNormalizer.upstream_payload(
              payload,
              model,
              "/backend-api/codex/responses",
              request_options
            )

          if payload == invalid_payload do
            assert {:error, %{status: 400, code: "invalid_request", param: "input"}} = result
          else
            assert {:ok, encoded} = result
            refute Map.has_key?(CodexPooler.JSON.decode!(encoded), "input")
          end
        end
      end
    end

    test "does not sanitize response item IDs for unrelated endpoints" do
      payload = %{
        "model" => "gpt-5.5",
        "input" => [%{"type" => "message", "id" => "msg-1", "content" => []}]
      }

      endpoint = "/backend-api/example/responses"

      request_options_by_transport = [
        http: RequestOptions.build(%{}, endpoint, payload),
        websocket: RequestOptions.build(%{transport: "websocket"}, endpoint, payload)
      ]

      model = %Model{upstream_model_id: "provider-model"}

      for {transport, request_options} <- request_options_by_transport do
        assert {:ok, encoded} =
                 PayloadNormalizer.upstream_payload(payload, model, endpoint, request_options)

        assert CodexPooler.JSON.decode!(encoded)["input"] == payload["input"],
               "unexpected #{transport} input"
      end
    end
  end

  defp encrypted_tool_schema_payload do
    %{
      "model" => "gpt-5.5",
      "input" => [%{"role" => "user", "content" => "hello"}],
      "tools" => [
        %{
          "type" => "function",
          "name" => "spawn_agent",
          "strict" => false,
          "parameters" => %{
            "type" => "object",
            "properties" => %{
              "message" => %{
                "type" => "string",
                "description" => "Initial plain-text task for the new agent.",
                "encrypted" => true
              },
              "task_name" => %{"type" => "string"}
            },
            "required" => ["task_name", "message"],
            "additionalProperties" => false
          }
        },
        %{
          "type" => "function",
          "function" => %{
            "name" => "send_message",
            "strict" => false,
            "parameters" => %{
              "type" => "object",
              "properties" => %{
                "message" => %{
                  "type" => "string",
                  "description" => "Message text to queue on the target agent.",
                  "encrypted" => true
                },
                "target" => %{"type" => "string"}
              },
              "required" => ["target", "message"],
              "additionalProperties" => false
            }
          }
        }
      ]
    }
  end

  defp schema_position_aware_tool_payload do
    %{
      "model" => "gpt-5.5",
      "input" => [%{"role" => "user", "content" => "hello"}],
      "tools" => [
        %{
          "type" => "function",
          "name" => "schema_position_fixture",
          "strict" => false,
          "parameters" => %{
            "type" => "object",
            "encrypted" => true,
            "properties" => %{
              "encrypted" => %{
                "type" => "object",
                "encrypted" => true,
                "properties" => %{"encrypted" => %{"type" => "string", "encrypted" => true}},
                "required" => ["encrypted"]
              },
              "nested" => %{
                "type" => "object",
                "properties" => %{"value" => %{"type" => "string", "encrypted" => true}}
              }
            },
            "required" => ["encrypted", "nested"],
            "$defs" => %{"encrypted" => %{"type" => "string", "encrypted" => true}},
            "definitions" => %{"encrypted" => %{"type" => "number", "encrypted" => true}},
            "items" => %{
              "type" => "object",
              "properties" => %{"encrypted" => %{"type" => "boolean", "encrypted" => true}}
            },
            "anyOf" => [
              %{
                "type" => "object",
                "properties" => %{"encrypted" => %{"type" => "string", "encrypted" => true}}
              }
            ],
            "oneOf" => [
              %{
                "type" => "object",
                "properties" => %{"encrypted" => %{"type" => "integer", "encrypted" => true}}
              }
            ],
            "allOf" => [
              %{
                "type" => "object",
                "properties" => %{"encrypted" => %{"type" => "null", "encrypted" => true}}
              }
            ]
          }
        }
      ]
    }
  end

  defp capture_continuation_state(payload, options) do
    assert {:ok, encoded, normalized_options} =
             PayloadNormalizer.prepare_upstream_payload(
               payload,
               %Model{upstream_model_id: "provider-model"},
               "/backend-api/codex/responses",
               options
             )

    parent = self()

    session =
      spawn_link(fn ->
        receive do
          {:"$gen_call", from, {:request, request}} ->
            send(parent, {:captured_upstream_websocket_request, request})

            GenServer.reply(
              from,
              {:ok, %{body: "", terminal: "response.completed", status: 200, headers: []}}
            )
        end
      end)

    request_options =
      RequestOptions.put_transport(normalized_options, upstream_websocket_session: session)

    assert {:ok, %{status: 200}} =
             UpstreamDispatch.websocket_request(%UpstreamDispatch.Request{
               url: "https://upstream.example.test/backend-api/codex/responses",
               token: "redacted",
               upstream_payload: encoded,
               identity: %UpstreamIdentity{},
               accounting_request: nil,
               writer: fn _message -> :ok end,
               assignment_advertised?: false,
               request_options: request_options
             })

    assert_receive {:captured_upstream_websocket_request, upstream_request}

    {
      normalized_options.continuity.upstream_previous_response_id?,
      upstream_request.connection_bound_continuation?
    }
  end

  defp ordinary_input,
    do: [%{"type" => "message", "role" => "user", "content" => "hello"}]

  defp tool_result_input,
    do: [%{"type" => "function_call_output", "call_id" => "call-fixture", "output" => "ok"}]

  defp standalone_tool_result_input,
    do: [%{"type" => "function_call_output", "name" => "lookup-fixture", "output" => "ok"}]

  defp non_strict_tool_schema_payload do
    %{
      "model" => "gpt-5.5",
      "input" => [%{"role" => "user", "content" => "hello"}],
      "tools" => [
        %{
          "type" => "function",
          "name" => "flat_lookup",
          "strict" => false,
          "parameters" => non_strict_tool_schema()
        },
        %{
          "type" => "function",
          "function" => %{
            "name" => "nested_lookup",
            "strict" => false,
            "parameters" => non_strict_tool_schema()
          }
        },
        %{
          "type" => "function",
          "name" => "strict_lookup",
          "strict" => true,
          "parameters" => non_strict_tool_schema()
        }
      ]
    }
  end

  defp backend_mixed_tool_payload(namespace_tool) do
    %{
      "model" => "gpt-5.5",
      "input" => [%{"role" => "user", "content" => "hello"}],
      "tools" => [
        namespace_tool,
        %{
          "type" => "function",
          "name" => "ordinary_lookup",
          "strict" => false,
          "encrypted" => true,
          "parameters" => backend_function_schema()
        }
      ]
    }
  end

  defp backend_namespace_tool do
    %{
      "type" => "namespace",
      "name" => "fixture_namespace",
      "description" => "Synthetic namespace tools",
      "encrypted" => true,
      "unknown_namespace_key" => %{"encrypted" => true, "preserve" => [1, nil, false]},
      "tools" => [
        %{
          "type" => "function",
          "name" => "namespaced_lookup",
          "strict" => false,
          "encrypted" => true,
          "parameters" => backend_function_schema(),
          "unknown_function_key" => %{"encrypted" => true}
        },
        %{
          "type" => "namespace",
          "name" => "nested_namespace",
          "tools" => [%{"type" => "future_tool", "encrypted" => true}],
          "unknown_nested_key" => true
        }
      ]
    }
  end

  defp backend_function_schema do
    %{
      "$schema" => "http://json-schema.org/draft-07/schema#",
      "properties" => %{
        "mode" => %{"const" => "fast", "title" => "drop me", "encrypted" => true},
        "nested" => %{
          "properties" => %{"value" => %{"type" => "string", "encrypted" => true}},
          "required" => ["value"],
          "encrypted" => true
        }
      },
      "required" => ["mode"],
      "additionalProperties" => false,
      "encrypted" => true
    }
  end

  defp lowered_backend_function_schema do
    %{
      "type" => "object",
      "properties" => %{
        "mode" => %{"enum" => ["fast"]},
        "nested" => %{
          "type" => "object",
          "properties" => %{"value" => %{"type" => "string"}},
          "required" => ["value"]
        }
      },
      "required" => ["mode"],
      "additionalProperties" => false
    }
  end

  defp non_strict_tool_schema do
    %{
      "$schema" => "http://json-schema.org/draft-07/schema#",
      "properties" => %{
        "mode" => %{"const" => "fast", "title" => "drop me"},
        "tags" => %{"items" => %{"const" => "tag"}},
        "nested" => %{
          "properties" => %{"ok" => true},
          "required" => ["ok"]
        },
        "choice" => %{
          "anyOf" => [
            %{"const" => "a"},
            %{"type" => "string", "default" => "drop me"}
          ]
        }
      },
      "required" => ["mode"],
      "additionalProperties" => %{"const" => "extra"},
      "$defs" => %{
        "Ref" => %{
          "properties" => %{"value" => %{"const" => "ref"}},
          "required" => ["value"]
        }
      },
      "definitions" => %{
        "Legacy" => %{"items" => %{"const" => "legacy"}}
      }
    }
  end

  defp lowered_tool_schema do
    %{
      "type" => "object",
      "properties" => %{
        "mode" => %{"enum" => ["fast"]},
        "tags" => %{"type" => "array", "items" => %{"enum" => ["tag"]}},
        "nested" => %{
          "type" => "object",
          "properties" => %{"ok" => %{}},
          "required" => ["ok"]
        },
        "choice" => %{
          "anyOf" => [
            %{"enum" => ["a"]},
            %{"type" => "string"}
          ]
        }
      },
      "required" => ["mode"],
      "additionalProperties" => %{"enum" => ["extra"]},
      "$defs" => %{
        "Ref" => %{
          "type" => "object",
          "properties" => %{"value" => %{"enum" => ["ref"]}},
          "required" => ["value"]
        }
      },
      "definitions" => %{
        "Legacy" => %{"type" => "array", "items" => %{"enum" => ["legacy"]}}
      }
    }
  end

  defp prepare_lite_payload(payload, endpoint \\ "/backend-api/codex/responses") do
    request_options = RequestOptions.build(serving_mode_opts("lite"), endpoint, payload)

    assert {:ok, encoded, _request_options} =
             PayloadNormalizer.prepare_upstream_payload(
               payload,
               %Model{upstream_model_id: "provider-model"},
               endpoint,
               request_options
             )

    CodexPooler.JSON.decode!(encoded)
  end

  defp prepare_incremental_lite_compact(source_payload) do
    compact_payload = CompactionTrigger.project_responses_payload(source_payload, :sse)

    request_options =
      "lite"
      |> serving_mode_opts()
      |> Map.merge(%{compaction_trigger_bridge?: true, compaction_result_transport: :sse})
      |> RequestOptions.build("/backend-api/codex/responses/compact", source_payload)

    model = %Model{upstream_model_id: "provider-model"}

    assert {:ok, first_encoded} =
             PayloadNormalizer.upstream_payload(
               compact_payload,
               model,
               "/backend-api/codex/responses/compact",
               request_options
             )

    first = CodexPooler.JSON.decode!(first_encoded)

    assert {:ok, second_encoded} =
             PayloadNormalizer.upstream_payload(
               first,
               model,
               "/backend-api/codex/responses/compact",
               request_options
             )

    {first, CodexPooler.JSON.decode!(second_encoded), request_options}
  end

  defp native_text_input(text) do
    [
      %{
        "type" => "message",
        "role" => "user",
        "content" => [%{"type" => "input_text", "text" => text}]
      }
    ]
  end

  defp serving_mode_opts(mode) when mode in ["lite", "full"] do
    %{
      model_serving_mode_configured: mode,
      model_serving_mode: mode,
      model_serving_mode_source: "override"
    }
  end

  defp remove_encrypted_markers(%{} = value) do
    value
    |> Map.delete("encrypted")
    |> Map.new(fn {key, nested} -> {key, remove_encrypted_markers(nested)} end)
  end

  defp remove_encrypted_markers(value) when is_list(value),
    do: Enum.map(value, &remove_encrypted_markers/1)

  defp remove_encrypted_markers(value), do: value

  defp strip_image_detail(%{"content" => content} = item) when is_list(content) do
    Map.put(item, "content", Enum.map(content, &strip_input_image_detail/1))
  end

  defp strip_image_detail(%{"output" => output} = item) when is_list(output) do
    Map.put(item, "output", Enum.map(output, &strip_input_image_detail/1))
  end

  defp strip_image_detail(%{"output" => %{"content" => content} = output} = item)
       when is_list(content) do
    Map.put(
      item,
      "output",
      Map.put(output, "content", Enum.map(content, &strip_input_image_detail/1))
    )
  end

  defp strip_input_image_detail(%{"type" => "input_image"} = content),
    do: Map.delete(content, "detail")

  defp strip_input_image_detail(content), do: content

  defp transcription_upload_fixture do
    path =
      Path.join(
        System.tmp_dir!(),
        "codex-pooler-normalizer-transcription-#{System.unique_integer([:positive])}.wav"
      )

    File.write!(path, "synthetic audio")
    on_exit(fn -> File.rm(path) end)
    path
  end

  defp content_shape(nil), do: nil
  defp content_shape(value) when is_binary(value), do: :binary
  defp content_shape(value) when is_list(value), do: :list

  defp prompt_cache_breakpoint_present?(%{} = value) do
    Map.has_key?(value, "prompt_cache_breakpoint") or
      Enum.any?(value, fn {_key, nested} -> prompt_cache_breakpoint_present?(nested) end)
  end

  defp prompt_cache_breakpoint_present?(value) when is_list(value),
    do: Enum.any?(value, &prompt_cache_breakpoint_present?/1)

  defp prompt_cache_breakpoint_present?(_value), do: false
end
